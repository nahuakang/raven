#+vet explicit-allocators shadowing
package raven

import "core:mem"
import "base:intrinsics"
import "core:strings"
import "core:log"
import "core:slice"
import "core:path/filepath"
import "core:math/linalg"
import "core:math"
import "core:fmt"
import "base:runtime"
import debug_trace "core:debug/trace"
import stbi "vendor:stb/image"

import "gpu"
import "platform"
import "rscn"
import "audio"
import "base"
import "base/ufmt"

// TODO: go through all TODOs

// TODO: fix triangles and pooled textures
// TODO: actual 3d transform structure
// TODO: objects in scene data
// TODO: asset_load and reload
// TODO: consistent get_* and no get API!
// TODO: font state
// TODO: audio
// TODO: separate hash table size from backing array size?
// TODO: abstract log_error and log_warn etc to comptime disable logging?
// TODO: compress vertex and instance data more
// TODO: More "summary" info when app exist - min/max/avg cpu/gpu frame time, num draws, temp allocs, ..?
// TODO: uniform anchor values
// TODO: drawing real lines, not quads
// TODO: default module init/shutdown procs

// ARTICLES
// - blog post about two level bit sets
// - open/container-less datastructures as building blocks
// - MM style functional timestep

RELEASE :: #config(RAVEN_RELEASE, false)
VALIDATION :: #config(RAVEN_VALIDATION, !RELEASE)

// Enable internal logs. Mostly useful for debugging internals.
LOG_INTERNAL :: #config(RAVEN_LOG_INTERNAL, false)

MAX_GROUPS :: 64
MAX_TEXTURES :: 256
MAX_MESHES :: 1024
MAX_OBJECTS :: 1024
MAX_SPLINES :: 1024

MAX_WATCHED_DIRS :: 8
MAX_DRAW_LAYERS :: 32
MAX_RENDER_TEXTURES :: 64
MAX_TEXTURE_RESOURCES :: 64
MAX_SHADERS :: 64
MAX_FILES :: 1024

MAX_TOTAL_SPRITE_INSTANCES :: 1024 * 32
MAX_TOTAL_MESH_INSTANCES :: 1024 * 64
MAX_TOTAL_TRIANGLE_INSTANCES :: 1024 * 8

MAX_TEXTURE_POOLS :: 8
MAX_TEXTURE_POOL_SLICES :: 64

MAX_BIND_STATE_DEPTH :: 64

MAX_TOTAL_DRAW_BATCHES :: 4096

// This is the actual swapchain used for rendering directly to screen.
DEFAULT_RENDER_TEXTURE :: Render_Texture_Handle{MAX_RENDER_TEXTURES - 1, 0}

HASH_SEED :: #config(RAVEN_HASH_SEED, 0xcbf29ce484222325)
MAX_PROBE_DIST :: #config(RAVEN_MAX_TABLE_PROBE_DIST, 16)

HASH_ALG :: "fnv64a"

Vec2 :: [2]f32
Vec3 :: [3]f32
Vec4 :: [4]f32
IVec2 :: [2]i32
IVec3 :: [3]i32
IVec4 :: [4]i32
Mat2 :: matrix[2, 2]f32
Mat3 :: matrix[3, 3]f32
Mat4 :: matrix[4, 4]f32
Quat :: quaternion128

Hash :: u64

HANDLE_INDEX_INVALID :: ~Handle_Index(0)

Handle_Index :: u16
Handle_Gen :: u8

Handle :: struct {
    index:  Handle_Index,
    gen:    Handle_Gen,
}

Group_Handle :: distinct Handle
Object_Handle :: distinct Handle
Mesh_Handle :: distinct Handle
Texture_Handle :: distinct Handle
Texture_Resource_Handle :: distinct Handle
Spline_Handle :: distinct Handle
Render_Texture_Handle :: distinct Handle
Vertex_Shader_Handle :: distinct Handle
Pixel_Shader_Handle :: distinct Handle

Rect :: struct {
    min:    Vec2,
    max:    Vec2,
}

#assert(len(Blend_Mode) <= 4)
// NOTE: if you want an Alpha Clip mode, you must do it yourself in a shader with 'discard'.
Blend_Mode :: enum u8 {
    Opaque, // No blending
    Premultiplied_Alpha, // For certain sprites.
    Alpha, // Regular alpha transparency.
    Add, // Additive blend mode, only makes things brighter.
}

#assert(len(Fill_Mode) <= 4)
Fill_Mode :: enum u8 {
    All, // Fill both front and back. Default for simplicity.
    Front, // Fill the default front side of the triangles.
    Back, // Inverted
    Wire, // Two-sided wireframe mode
}

Sprite_Scaling :: enum u8 {
    // Sprite scale determines the pixel scaling factor.
    // Scale of 1 means each pixel is exactly one screen pixel.
    Pixel = 0,
    // No scaling, sprite scale is the final scale in pixels
    // Scale of 1 means the ENTIRE sprite is 1x1 pixels.
    Absolute,
}

_state: ^State

State :: struct #align(64) {
    initialized:            bool,
    start_time:             u64,
    curr_time:              u64,
    last_time:              u64,
    frame_dur_ns:           u64,
    frame_index:            u64,
    screen_size:            [2]i32,
    screen_dirty:           bool,
    allocator:              runtime.Allocator,
    window:                 platform.Window,
    dpi_scale:              f32,
    module_result:          rawptr,
    module_api:             Module_API,

    debug_trace_ctx:        debug_trace.Context,
    context_state:          Context_State,

    uploaded_gpu_draws:     bool,

    input:                  Input,

    bind_state:             Bind_State,
    bind_states:            [MAX_BIND_STATE_DEPTH]Bind_State,
    bind_states_len:        i32,

    default_group:          Group_Handle,
    default_mesh:           Mesh_Handle,
    default_texture:        Texture_Handle,
    default_font_texture:   Texture_Handle,
    error_texture:          Texture_Handle,
    default_sprite_vs:      Vertex_Shader_Handle,
    default_vs:             Vertex_Shader_Handle,
    default_ps:             Pixel_Shader_Handle,

    sprite_inst_buf:        gpu.Resource_Handle,
    mesh_inst_buf:          gpu.Resource_Handle,
    triangle_vbuf:          gpu.Resource_Handle,
    quad_ibuf:              gpu.Resource_Handle,

    global_consts:          gpu.Resource_Handle,
    draw_layers_consts:     gpu.Resource_Handle,
    draw_batch_consts:      gpu.Resource_Handle,

    counters:               [Counter_Kind]Counter_State,

    watched_dirs_num:       i32,
    watched_dirs:           [MAX_WATCHED_DIRS]Watched_Dir,

    draw_layers:            [MAX_DRAW_LAYERS]Draw_Layer,

    groups_used:            bit_set[0..<MAX_GROUPS],
    groups_gen:             [MAX_GROUPS]Handle_Gen,
    groups:                 [MAX_GROUPS]Group,

    render_textures_used:   bit_set[0..<MAX_RENDER_TEXTURES],
    render_textures_gen:    [MAX_RENDER_TEXTURES]Handle_Gen,
    render_textures:        [MAX_RENDER_TEXTURES]Render_Texture,

    objects_hash:           [MAX_OBJECTS]Hash,
    objects_gen:            [MAX_OBJECTS]Handle_Gen,
    objects:                [MAX_OBJECTS]Object,

    meshes_hash:            [MAX_MESHES]Hash,
    meshes_gen:             [MAX_MESHES]Handle_Gen,
    meshes:                 [MAX_MESHES]Mesh,

    splines_hash:           [MAX_SPLINES]Hash,
    splines_gen:            [MAX_SPLINES]Handle_Gen,
    splines:                [MAX_SPLINES]Spline,

    textures_hash:          [MAX_TEXTURES]Hash,
    textures_gen:           [MAX_TEXTURES]Handle_Gen,
    textures:               [MAX_TEXTURES]Texture,
    texture_pools:          [MAX_TEXTURE_POOLS]Texture_Pool,
    texture_pools_len:      i32,

    pixel_shaders_hash:     [MAX_SHADERS]Hash,
    pixel_shaders_gen:      [MAX_SHADERS]Handle_Gen,
    pixel_shaders:          [MAX_SHADERS]Pixel_Shader,

    vertex_shaders_hash:    [MAX_SHADERS]Hash,
    vertex_shaders_gen:     [MAX_SHADERS]Handle_Gen,
    vertex_shaders:         [MAX_SHADERS]Vertex_Shader,

    files_hash:             [MAX_FILES]Hash,
    files:                  [MAX_FILES]File,

    platform_state:         platform.State,
    gpu_state:              gpu.State,
    audio_state:            audio.State,
}

Context_State :: struct {
    logger:     log.File_Console_Logger_Data, // TODO: replace by custom
    tracking:   mem.Tracking_Allocator,
}

// VFS file
File :: struct {
    flags:          bit_set[File_Flag],
    watched_dir:    u8,
    data:           []byte,
}

File_Flag :: enum u8 {
    Dirty,
    Changed, // Waiting to get loaded
    Dynamically_Allocated, // must use _state.allocator
}

Watched_Dir :: struct {
    path_len:   i32,
    path:       [256]byte,
    watcher:    platform.File_Watcher,
}

Pixel_Shader :: distinct Shader
Vertex_Shader :: distinct Shader
Shader :: struct {
    shader: gpu.Shader_Handle,
}


// Data Scope
// Collection of data with one lifetime.
Group :: struct {
    spline_vert_num:    i32,
    mesh_vert_num:      i32,
    mesh_index_num:     i32,
    object_child_num:   i32,

    object_buf:         []Object,
    object_child_buf:   []Object_Handle,
    spline_vert_buf:    []Spline_Vertex,

    vbuf:               gpu.Resource_Handle,
    ibuf:               gpu.Resource_Handle,
}

Object_Kind :: rscn.Object_Kind

// TODO: objects in general are totally unfinished
Object :: struct {
    kind:               Object_Kind,

    // TODO
    // Format like "Enemy:Foo0"?
    name_prefix:        Hash,
    name:               [16]u8,

    group:              Group_Handle,

    // Depends on 'kind' - either mesh or spline handle.
    data_handle:        Handle,
    texture:            Texture_Handle,

    parent:             Object_Handle,
    child_offset:       i32,
    child_num:          i32,

    param:              u64, // user param

    local_pos:          Vec3,
    local_rot:          Mat3,
    local_scale:        Vec3,
}

Mesh :: struct {
    group:          Group_Handle,

    vert_num:       i32,
    vert_offs:      i32,
    index_num:      i32,
    index_offs:     i32,

    param:          u64, // user param

    bounds_min:     Vec3,
    bounds_max:     Vec3,
}

Spline :: struct {
    group:          Group_Handle,

    vert_num:       i32,
    vert_offs:      i32,

    param:          u64, // user param

    bounds_min:     Vec3,
    bounds_max:     Vec3,
}


Vertex_Index :: u16 // GPU Vertex Index
Spline_Vertex :: rscn.Spline_Vertex

Mesh_Vertex :: struct #align(16) {
    pos:    [3]f32,
    _pad:   f32,
    uv:     [2]f32,
    normal: [3]u8 `gpu:"normalized"`,
    p0:     u8, // NOTE: this padding could store user parameters..?
    color:  [3]u8 `gpu:"normalized"`,
    p1:     u8,
}


Texture_Pool :: struct {
    slices_used:    bit_set[0..<MAX_TEXTURE_POOL_SLICES],
    size:           IVec2,
    slices:         i32,
    resource:       gpu.Resource_Handle,
}

Texture :: struct {
    size:       [2]u16,
    pool_index: u8,
    slice:      u8,
    resource:   gpu.Resource_Handle,
}

Texture_Data :: struct {
    size:   [2]i32,
    pixels: [][4]u8,
}


Bind_State :: struct {
    draw_layer:             u8,
    blend:                  Blend_Mode,
    fill:                   Fill_Mode,
    depth_test:             bool,
    depth_write:            bool,
    texture_mode:           Bind_Texture_Mode,
    texture:                u8,
    texture_slice:          u8,
    texture_size:           [2]u16, // cached
    pixel_shader:           u8,
    vertex_shader:          u8,
    sprite_scaling:         Sprite_Scaling,
}

Bind_Texture_Mode :: enum u8 {
    Non_Pooled,
    Pooled,
    Render_Texture,
}

Draw_Layer :: struct {
    camera:                 Camera,
    flags:                  bit_set[Draw_Layer_Flag],

    sprite_insts_base:      u32,
    mesh_insts_base:        u32,
    triangle_insts_base:    u32,

    last_sprites_len:       i32,
    last_meshes_len:        i32,
    last_triangles_len:     i32,

    // NOTE: the dynamic arrays must be allocated with temp_allocator.
    // Beware of the default append() behavior.

    sprites:                #soa[dynamic]Sprite_Draw,
    meshes:                 #soa[dynamic]Mesh_Draw,
    triangles:              #soa[dynamic]Triangle_Draw,

    sprite_batches:         [dynamic]Draw_Batch,
    mesh_batches:           [dynamic]Draw_Batch,
    triangle_batches:       [dynamic]Draw_Batch,
}

Draw_Layer_Flag :: enum u8 {
    // Disable frustum culling.
    No_Cull,
    // Disable all sorting.
    // NOTE: this doesn't affect just transparent objects, it's how batching optimization is done.
    No_Reorder,
}

// Shared across all layers and everything.
Draw_Global_Constants :: struct #all_or_none #align(16) {
    time:           f32,
    delta_time:     f32,
    frame:          u32,
    resolution:     [2]i32,
    rand_seed:      u32,
    param0:         u32,
    param1:         u32,
    param2:         u32,
    param3:         u32,
}

Draw_Layer_Constants :: struct #all_or_none #align(16) {
    view_proj:      Mat4,
    cam_pos:        Vec3,
    layer_index:    i32,
}

#assert(size_of(Draw_Batch_Constants) == 16)
Draw_Batch_Constants :: struct #align(16) {
    instance_offset:    u32,
}


Render_Texture :: struct #all_or_none {
    size:   IVec2,
    color:  gpu.Resource_Handle,
    depth:  gpu.Resource_Handle,
}

// (CPU) Draw instance data

Sprite_Draw :: struct #all_or_none {
    key:    Draw_Sort_Key,
    inst:   Sprite_Inst,
}

Mesh_Draw :: struct #all_or_none {
    key:    Draw_Sort_Key,
    inst:   Mesh_Inst,
    extra:  struct {
        index:  u16,
    },
}

Triangle_Draw :: struct #all_or_none {
    key:    Draw_Sort_Key,
    verts:  [3]Mesh_Vertex,
}


// CPU Data for a single draw call.
Draw_Batch :: struct #all_or_none {
    key:            Draw_Sort_Key,
    offset:         u32,
    num:            u16,
}

// GPU Instance data

#assert(size_of(Sprite_Inst) == 64)
Sprite_Inst :: struct #all_or_none #align(16) {
    pos:        [3]f32,
    color:      [4]u8,
    mat_x:      [3]f32,
    uv_min_x:   f32,
    mat_y:      [3]f32,
    uv_min_y:   f32,
    uv_size:    [2]f32,
    tex_slice:  u8,
}

Mesh_Inst :: struct #all_or_none #align(16) {
    pos:        Vec3,
    _:          f32,
    mat_x:      Vec3,
    _:          f32,
    mat_y:      Vec3,
    _:          f32,
    mat_z:      Vec3,
    _:          f32,
    col:        [4]u8,
    vert_offs:  u32,
    tex_slice:  u8,
    _pad0:      [3]u8,
    param:      u32, // user param
}

#assert(MAX_TEXTURES <= 256)
#assert(MAX_SHADERS <= 64)
#assert(MAX_GROUPS <= 64)

DRAW_SORT_DIST_BITS :: 14
MAX_DRAW_SORT_KEY_DIST :: (1 << DRAW_SORT_DIST_BITS) - 1

// NOTE: the sort distance could be packed in fewer bits.
// HACK: TODO: u128 is stupid. This entire structure needs to be smaller.
// Order of batches is defined bottom-up by these fields.
Draw_Sort_Key :: bit_field u128 {
    texture:        u8 | 8,
    texture_mode:   Bind_Texture_Mode | 2,
    dist:           u16 | DRAW_SORT_DIST_BITS,
    fill:           Fill_Mode | 2,
    depth_write:    bool | 1,
    depth_test:     bool | 1,
    group:          u8 | 6,
    ps:             u8 | 6,
    vs:             u8 | 6,
    index_num:      u16 | 16,
    index_offs:     u32 | 32,
    blend:          Blend_Mode | 2,
}

draw_sort_key_equal :: proc(key_a, key_b: Draw_Sort_Key) -> bool {
    a := key_a
    b := key_b
    a.dist = 0
    b.dist = 0
    return a == b
}

DEFAULT_SAMPLERS :: [2]gpu.Sampler_Desc{
    0 = {
        filter = .Unfiltered,
        bounds = {.Wrap, .Wrap, .Wrap},
        mip_max = 10,
    },
    // 1 = {
    //     filter = .Filtered,
    //     bounds = .Wrap,
    //     mip_max = 10,
    // },
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Base
//

set_state_ptr :: proc "contextless" (state: ^State) {
    _state = state
    platform._state = &_state.platform_state
    gpu._state = &_state.gpu_state
    audio._state = &_state.audio_state
}

get_state_ptr :: proc "contextless" () -> (state: ^State) {
    return _state
}

@(require_results)
is_initialized :: proc "contextless" () -> bool {
    if _state == nil {
        return false
    }

    return _state.initialized
}

Init_Proc ::       #type proc() -> rawptr
Shutdown_Proc ::   #type proc(rawptr)
Update_Proc ::     #type proc(rawptr) -> rawptr

Module_API :: struct {
    state_size: i64,
    init:       Init_Proc,
    shutdown:   Shutdown_Proc,
    update:     Update_Proc,
}

// Default runner for a raven app.
// This is optional, but it's a good default.
// Calling this does nothing when compiling as a DLL, it's the responsibility
// of whoever loaded the DLL (e.g. hotreload runner) to call the app.
// NOTE: Things like reload never get called in this mode.
run_main_loop :: proc(api: Module_API) {
    ensure(api.init != nil)
    ensure(api.update != nil)

    when ODIN_BUILD_MODE == .Dynamic {

        // Nothing

    } else when ODIN_OS == .JS {

        init_state(context.allocator)

        context = get_context()

        _state.module_api = api
        _state.module_result = api.init()

    } else when ODIN_OS == .Windows || ODIN_OS == .Linux || ODIN_OS == .Darwin {

        init_state(context.allocator)

        context = get_context()

        _state.module_result = api.init()

        if _state.module_result == nil {
            return
        }

        ensure(_state.window != {}, "Init procedure must create a window")
        ensure(_state.gpu_state.fully_initialized)

        for {
            if !begin_frame() {
                break
            }

            res := api.update(_state.module_result)

            if res == nil {
                break
            }

            end_frame()

            _state.module_result = res
        }

        if api.shutdown != nil && _state.module_result != nil {
            api.shutdown(_state.module_result)
        }

        shutdown_state()

    } else {
        panic("Cannot run module loop on this platform")
    }
}

when ODIN_OS == .JS {
    @(export)
    step :: proc(dt: f32) -> (keep_running: bool) {
        log.info("Step")

        assert(_state != nil)
        assert(_state.module_api.update != nil)
        assert(_state.module_api.init != nil)

        // In case init returned nil
        if _state.module_result == nil {
            return false
        }

        context = get_context()

        if !_state.initialized {
            if _state.gpu_state.fully_initialized {
                _finish_init()
            } else {
                return true
            }
        }

        prev_result := _state.module_result

        if !begin_frame() {
            _state.module_api.shutdown(prev_result)
            return false
        }

        _state.module_result = _state.module_api.update(_state.module_result)

        if _state.module_result == nil {
            _state.module_api.shutdown(prev_result)
            return false
        }

        end_frame()

        return true
    }
} else when ODIN_BUILD_MODE == .Dynamic {
    @(export)
    _module_hot_step :: proc "contextless" (prev_state: ^State, api: Module_API) -> ^State {
        if prev_state == nil {
            // First init

            context = runtime.default_context()

            fmt.println("raven hot step: init")

            init_state(context.allocator)

            context = get_context()

            ensure(_state != nil)

            _state.module_result = api.init()

            ensure(_state.window != {}, "Init procedure must create a window")
            ensure(_state.gpu_state.fully_initialized)

            return _state

        } else if _state == nil {
            // Hot-reload
            set_state_ptr(prev_state)

            context = runtime.default_context()
            fmt.println("raven hot step: reload")

            return _state
        }

        // Regular frame

        if _state.module_result == nil {
            return nil
        }

        context = get_context()

        prev_result := _state.module_result

        if !begin_frame() {
            if _state.module_api.shutdown != nil {
                _state.module_api.shutdown(prev_result)
            }
            return nil
        }

        _state.module_result = api.update(_state.module_result)

        if _state.module_result == nil {
            if _state.module_api.shutdown != nil {
                _state.module_api.shutdown(prev_result)
            }
            return nil
        }

        end_frame()

        return _state
    }
}


get_context :: proc "contextless" () -> (result: runtime.Context) {
    result = runtime.default_context()

    result.assertion_failure_proc = _assertion_failure_proc

    result.allocator = {
        procedure = mem.tracking_allocator_proc,
        data = &_state.context_state.tracking,
    }

    result.logger = runtime.Logger{
        procedure = log.console_logger_proc,
        data = &_state.context_state.logger,
        lowest_level = .Debug,
        options = {.Level, .Time, .Short_File_Path, .Line, .Procedure, .Terminal_Color},
    }

    return result
}

init_context_state :: proc(ctx: ^Context_State) {
    _state.context_state.logger = {
        file_handle = auto_cast(~uintptr(0)),
        ident = "",
    }

    mem.tracking_allocator_init(&_state.context_state.tracking, context.allocator, context.allocator)
}

// Create state, init context, init subsystems.
init_state :: proc(allocator := context.allocator) {
    ensure(_state == nil)

    state_err: runtime.Allocator_Error
    _state, state_err = new(State, allocator = allocator)

    if state_err != nil {
        panic("Failed to allocate Raven State")
    }

    _state.allocator = allocator

    init_context_state(&_state.context_state)

    context = get_context()

    platform.init(&_state.platform_state)
    platform.set_dpi_aware()
    _state.start_time = platform.get_time_ns()

    audio.init(&_state.audio_state)

    debug_trace.init(&_state.debug_trace_ctx)

    for &counter in _state.counters {
        counter.total_min = max(u64)
        counter.accum = max(u64)
    }
}

// No-op if already initialized.
init_window :: proc(name := "Raven App", style: platform.Window_Style = .Regular, allocator := context.allocator) {
    ensure(_state != nil)
    ensure(_state.window == {})
    assert(platform._state != nil)
    assert(audio._state != nil)

    _state.window = platform.create_window(name, style = style)

    gpu.init(&_state.gpu_state, platform.get_native_window_ptr(_state.window))

    // TODO: more safety checking in the entire init function

    if ODIN_OS == .Windows {
        assert(_state.gpu_state.fully_initialized)
    }

    if _state.gpu_state.fully_initialized {
        _finish_init()
    }
}

_finish_init :: proc() {
    assert(_state != nil)

    _state.screen_size = platform.get_window_frame_rect(_state.window).size
    _state.screen_dirty = true

    pool128_ok := create_texture_pool(128, 64)
    assert(pool128_ok)

    // Swapchain
    _state.render_textures_used += {int(DEFAULT_RENDER_TEXTURE.index)}
    _state.render_textures_gen[DEFAULT_RENDER_TEXTURE.index] = DEFAULT_RENDER_TEXTURE.gen
    _state.render_textures[DEFAULT_RENDER_TEXTURE.index] = Render_Texture{
        size = _state.screen_size,
        color = {},
        depth = gpu.create_texture_2d("rv-def-rentex-depth", .D_F32, _state.screen_size, render_texture = true) or_else panic("gpu"),
    }

    _state.default_group = create_group(
        max_mesh_verts = 1024,
        max_mesh_indices = 1024,
        max_spline_verts = 0,
        max_total_children = 0,
    ) or_else panic("Default group")

    _state.sprite_inst_buf = gpu.create_buffer("rv-sprite-inst-buf",
        stride = size_of(Sprite_Inst),
        size = size_of(Sprite_Inst) * MAX_TOTAL_SPRITE_INSTANCES,
        usage = .Dynamic,
    ) or_else panic("gpu")

    _state.triangle_vbuf = gpu.create_buffer("rv-triangle-vbuf",
        stride = size_of(Mesh_Vertex),
        size = size_of(Mesh_Vertex) * 3 * MAX_TOTAL_TRIANGLE_INSTANCES,
        usage = .Dynamic,
    ) or_else panic("gpu")


    _state.mesh_inst_buf = gpu.create_buffer("rv-mesh-inst-buf",
        stride = size_of(Mesh_Inst),
        size = size_of(Mesh_Inst) * MAX_TOTAL_MESH_INSTANCES,
        usage = .Dynamic,
    ) or_else panic("gpu")

    _state.global_consts = gpu.create_constants("rv-global-consts",
        size_of(Draw_Global_Constants),
    ) or_else panic("gpu")

    _state.draw_batch_consts = gpu.create_constants("rv-batch-consts",
        size_of(Draw_Batch_Constants),
        MAX_TOTAL_DRAW_BATCHES,
    ) or_else panic("gpu")

    _state.draw_layers_consts = gpu.create_constants("rv-layer-consts",
        size_of(Draw_Layer_Constants),
        MAX_DRAW_LAYERS,
    ) or_else panic("gpu")

    quad_indices := [6]u16{
        0, 1, 2,
        1, 3, 2,
    }

    _state.quad_ibuf = gpu.create_index_buffer("rv-quad-index-buf", data = gpu.slice_bytes(quad_indices[:])) or_else panic("gpu")


    _state.default_texture = create_texture_from_encoded_data(
        "default",
        #load("data/default.png"),
    ) or_else panic("Failed to load default texture")

    _ = create_texture_from_encoded_data(
        "white",
        #load("data/white.png"),
    ) or_else panic("Failed to load default texture")

    _ = create_texture_from_encoded_data(
        "uv_tex",
        #load("data/uv_tex.png"),
    ) or_else panic("Failed to load default texture")

    _state.error_texture = create_texture_from_encoded_data(
        "error",
        #load("data/error.png"),
    ) or_else panic("Failed to load error texture")

    _state.default_font_texture = create_texture_from_encoded_data(
        "thick",
        #load("data/CGA8x8thick.png"),
    ) or_else panic("Failed to load default font texture")

    _ = create_texture_from_encoded_data(
        "thin",
        #load("data/CGA8x8thin.png"),
    ) or_else panic("Failed to load default font texture")

    default_sprite_vs: []byte
    default_vs: []byte
    default_ps: []byte

    switch gpu.BACKEND {
    case gpu.BACKEND_D3D11:

        INCL :: #load("data/raven.hlsli", string)

        default_sprite_vs = transmute([]byte)(INCL + #load("data/default_sprite.vs.hlsl", string))
        default_vs = transmute([]byte)(INCL + #load("data/default.vs.hlsl", string))
        default_ps = transmute([]byte)(INCL + #load("data/default.ps.hlsl", string))

    case gpu.BACKEND_WGPU:

        INCL :: #load("data/raven.wgsl", string)

        default_sprite_vs = transmute([]byte)(INCL + #load("data/default_sprite.vs.wgsl", string))
        default_vs = transmute([]byte)(INCL + #load("data/default.vs.wgsl", string))
        default_ps = transmute([]byte)(INCL + #load("data/default.ps.wgsl", string))

    case:
        panic("GPU backend not supported or unknown")
    }

    _state.default_sprite_vs = create_vertex_shader("default_sprite", default_sprite_vs) or_else panic("Failed to load default sprite vertex shader")
    _state.default_vs = create_vertex_shader("default", default_vs) or_else panic("Failed to load default vertex shader")
    _state.default_ps = create_pixel_shader("default", default_ps) or_else panic("Failed to load default pixel shader")

    _state.default_group = load_scene_from_data(
        #load("data/default.rscn", string),
        #load("data/default.rscn.bin"),
        dst_group = {},
    ) or_else panic("Failed to load default scene")

    log.info("Raven initialized successfully")

    _state.initialized = true
}

shutdown_state :: proc() {
    if _state == nil {
        return
    }

    _print_stats_report()

    audio.shutdown()
    gpu.shutdown()
    platform.shutdown()

    free(_state, _state.allocator)
    _state = nil
}

_print_stats_report :: proc() {
    fmt.println("Stats Report:")

    {
        c := _state.counters[.CPU_Frame_Ns]
        fmt.printfln("CPU Frame time (ms):          avg %.3f, min %.3f, max %.3f",
            f64(c.total_sum) * 1e-6 / f64(c.total_num),
            f64(c.total_min) * 1e-6,
            f64(c.total_max) * 1e-6,
        )
    }

    {
        tot := _state.counters[.Num_Total_Instances]
        upl := _state.counters[.Num_Uploaded_Instances]
        fmt.printfln("Per Frame Draw Instances:     avg total %.3f, avg uploaded %.3f",
            f64(tot.total_sum) / f64(tot.total_num),
            f64(upl.total_sum) / f64(upl.total_num),
        )
    }

    {
        c := _state.counters[.Num_Draw_Calls]
        fmt.printfln("Draw Calls:                   avg %.3f, min %i, max %i",
            f64(c.total_sum) / f64(c.total_num),
            c.total_min,
            c.total_max,
        )
    }

    {
        tr := _state.context_state.tracking
        fmt.printfln("Allocations:                  %i, %i freed, %i bytes total", tr.total_allocation_count, tr.total_free_count, tr.total_memory_allocated)

        if len(tr.allocation_map) > 0 {
            fmt.printfln("Memory Leaks:")
            for addr, it in tr.allocation_map {
                fmt.printfln("\t[{0}:{1}:{2}:{3}] Leaked {4:p} of size {5:M} ({5} bytes) with alignment {6:M}",
                    it.location.file_path,
                    it.location.procedure,
                    it.location.line,
                    it.location.column,
                    it.memory,
                    it.size,
                    it.alignment,
                )
            }
            fmt.println("\tTotal Memory Leaks:", len(tr.allocation_map))
        }

        if len(tr.bad_free_array) > 0 {
            fmt.println("Bad Frees:")
            for it in tr.bad_free_array {
                fmt.println("\t[{0}:{1}:{2}:{3}] Freed invalid {4:p}",
                    it.location.file_path,
                    it.location.procedure,
                    it.location.line,
                    it.location.column,
                    it.memory,
                )
            }
            fmt.println("\tTotal Bad Frees:", len(tr.bad_free_array))
        }

        peak_mem := tr.peak_memory_allocated + size_of(State)
        fmt.printfln("Peak memory:                  %i bytes (%.3f MB) ", peak_mem, f64(peak_mem) / (1024 * 1024))
    }


}

begin_frame :: proc() -> (keep_running: bool) {
    assert(_state != nil)

    keep_running = true

    free_all(context.temp_allocator)
    // In case big file allocations happened...
    defer free_all(context.temp_allocator)

    prev_screen_size := _state.screen_size
    screen := platform.get_window_frame_rect(_state.window).size
    if screen.x > 0 && screen.y > 0 {
        _state.screen_size = screen
    }

    if prev_screen_size != _state.screen_size {
        _state.screen_dirty = true
    }

    if _state.screen_dirty {
        _state.screen_dirty = false
        assert(_state.render_textures_gen[DEFAULT_RENDER_TEXTURE.index] == DEFAULT_RENDER_TEXTURE.gen)
        rt := &_state.render_textures[DEFAULT_RENDER_TEXTURE.index]
        gpu.destroy_resource(rt.depth)
        rt.size = _state.screen_size
        rt.depth = gpu.create_texture_2d("rv-def-rentex-depth", .D_F32, _state.screen_size, render_texture = true) or_else panic("gpu")
        rt.color = gpu.update_swapchain(platform.get_native_window_ptr(_state.window), _state.screen_size) or_else panic("gpu")
    }

    assert(_state.render_textures[DEFAULT_RENDER_TEXTURE.index].color != {})

    for &counter in _state.counters {
        _counter_flush(&counter)
    }

    gpu_can_begin_frame := gpu.begin_frame()
    assert(gpu_can_begin_frame)

    audio.update()

    assert(_state.bind_states_len == 0, "Looks like you forgot pop_binds() somewhere")
    _state.frame_index += 1
    _state.uploaded_gpu_draws = false

    time_ns := platform.get_time_ns()
    _state.curr_time = time_ns

    _state.frame_dur_ns = time_ns - _state.last_time

    _state.last_time = time_ns

    if _state.frame_index > 10 {
        _counter_add(.CPU_Frame_Ns, _state.frame_dur_ns)
    } else {
        _counter_add(.CPU_Frame_Ns, max(u64))
    }

    _clear_draw_layers()

    _state.dpi_scale = platform.window_dpi_scale(_state.window)
    // log.info("DPI scale: ", _state.dpi_scale)

    _state.input.mouse_delta = 0
    _state.input.scroll_delta = 0

    delta := get_delta_time()
    _begin_input_digital_buffer_frame(&_state.input.keys, delta)
    _begin_input_digital_buffer_frame(&_state.input.mouse_buttons, delta)
    for &gp in _state.input.gamepads {
        _begin_input_digital_buffer_frame(&gp.buttons, delta)
        gp.axes = {}
    }

    for event in platform.poll_window_events(_state.window) {
        switch v in event {
        case platform.Event_Exit:
            keep_running = false

        case platform.Event_Key:
            if v.pressed {
                _input_digital_press(&_state.input.keys, v.key)
            } else {
                _input_digital_release(&_state.input.keys, v.key)
            }

        case platform.Event_Mouse_Button:
            if v.pressed {
                _input_digital_press(&_state.input.mouse_buttons, v.button)
            } else {
                _input_digital_release(&_state.input.mouse_buttons, v.button)
            }

        case platform.Event_Mouse:
            _state.input.mouse_delta.x += f32(v.move.x)
            _state.input.mouse_delta.y += f32(-v.move.y)
            _state.input.mouse_pos.x = f32(v.pos.x)
            _state.input.mouse_pos.y = f32(_state.screen_size.y - v.pos.y)

        case platform.Event_Scroll:
            _state.input.scroll_delta += v.amount

        case platform.Event_Window_Size:
        }
    }

    for i in 0..<MAX_GAMEPADS {
        inp, inp_ok := platform.get_gamepad_state(i)
        if !inp_ok {
            _state.input.gamepads[i] = {}
            _state.input.gamepads_connected -= {i}
        }

        _state.input.gamepads_connected += {i}

        gpad := &_state.input.gamepads[i]

        for btn in Gamepad_Button {
            if btn in inp.buttons {
                _input_digital_press(&gpad.buttons, btn)
            } else {
                _input_digital_release(&gpad.buttons, btn)
            }
        }

        gpad.buttons.released = {}

        gpad.axes[.Left_Trigger] = inp.axes[.Left_Trigger] > 0.1 ? clamp(gpad.axes[.Left_Trigger], 0, 1) : 0
        gpad.axes[.Right_Trigger] = inp.axes[.Right_Trigger] > 0.1 ? clamp(gpad.axes[.Right_Trigger], 0, 1) : 0

        l_thumb := Vec2{
            gpad.axes[.Left_Thumb_X],
            gpad.axes[.Left_Thumb_Y],
        }

        r_thumb := Vec2{
            gpad.axes[.Right_Thumb_X],
            gpad.axes[.Right_Thumb_Y],
        }

        l_len := linalg.length(l_thumb)
        r_len := linalg.length(r_thumb)

        if l_len < 0.1 {
            l_thumb = 0
        } else if l_len > 1 {
            l_thumb = l_thumb / l_len
        }

        if r_len < 0.1 {
            r_thumb = 0
        } else if r_len > 1 {
            r_thumb = r_thumb / r_len
        }

        gpad.axes[.Left_Thumb_X] = l_thumb.x
        gpad.axes[.Left_Thumb_Y] = l_thumb.y
        gpad.axes[.Right_Thumb_X] = r_thumb.x
        gpad.axes[.Right_Thumb_Y] = r_thumb.y
    }

    if _state.frame_index < 5 {
        _state.input.mouse_delta = 0
    }

    changed_files := make([dynamic]string, 0, 64, context.temp_allocator)

    for i in 0..<_state.watched_dirs_num {
        dir := &_state.watched_dirs[i]

        path := string(dir.path[:dir.path_len])

        changes := platform.watch_file_changes(&dir.watcher)

        for change in changes {
            log.info("changed file:", change)

            file_path := filepath.join({path, change}, context.temp_allocator)

            data, ok := platform.read_file_by_path(file_path, allocator = _state.allocator)

            if !ok {
                log_internal("Failed to hotreload file {}", file_path)
                continue
            }

            if file, file_ok := get_internal_file_by_hash(hash_name(change)); file_ok {
                append(&changed_files, change)

                if .Dynamically_Allocated in file.flags {
                    delete(file.data, _state.allocator)
                }

                file.flags += {.Dirty, .Dynamically_Allocated}
                file.data = data
            } else {
                // NEW file
                create_file_by_name(change, data, flags = {.Dynamically_Allocated})
            }
        }
    }

    for change in changed_files {
        load_asset(change, {})
    }

    _state.bind_states_len = 0
    _state.bind_state = {
        pixel_shader = int_cast(u8, _state.default_ps.index),
        vertex_shader = int_cast(u8, _state.default_vs.index),
        blend = .Opaque,
        texture = u8(_state.default_texture.index),
    }

    bind_pixel_shader_by_handle({})
    bind_vertex_shader_by_handle({})
    bind_texture_by_handle({})


    return keep_running
}

end_frame :: proc(vsync := true) {

    curr_time := platform.get_time_ns()

    frame_work_dur_ns := curr_time - _state.last_time

    if _state.frame_index > 10 {
        _counter_add(.CPU_Frame_Work_Ns, frame_work_dur_ns)
    } else {
        _counter_add(.CPU_Frame_Work_Ns, max(u64))
    }

    gpu.end_frame(sync = vsync)
}

_clear_draw_layers :: proc() {
    for &layer in _state.draw_layers {
        layer.last_sprites_len = i32(len(layer.sprites))
        layer.last_meshes_len = i32(len(layer.meshes))
        layer.last_triangles_len = i32(len(layer.triangles))

        layer.sprites = {}
        layer.meshes = {}
        layer.triangles = {}

        layer.sprite_batches = {}
        layer.mesh_batches = {}
        layer.triangle_batches = {}
    }
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Util
//

// Frame's delta time
@(require_results)
get_delta_time :: proc() -> f32 {
    return f32(f64(_state.frame_dur_ns) * 1e-9)
}

@(require_results)
get_frame_index :: proc() -> u64 {
    return _state.frame_index
}

@(require_results)
get_time :: proc() -> f32 {
    return f32(f64(_state.curr_time - _state.start_time) * 1e-9)
}

@(require_results)
atlas_cell :: proc(split: [2]i32, coord: [2]i32, scale: [2]f32 = 1.0) -> Rect {
    validate(split.x >= 1)
    validate(split.y >= 1)

    p := Vec2{
        linalg.fract(f32(coord.x) / f32(split.x)),
        linalg.fract(f32(coord.y) / f32(split.y)),
    }

    result := Rect{
        min = p,
        max = p + {
            scale.x / f32(split.x),
            scale.y / f32(split.y),
        },
    }

    result.min.y = 1.0 - result.min.y
    result.max.y = 1.0 - result.max.y

    return result
}

@(require_results)
atlas_slot :: proc(split: [2]i32, #any_int index: i32) -> Rect {
    validate(split.x >= 1)
    validate(split.y >= 1)

    coord := [2]i32{
        index % split.x,
        index / split.x,
    }

    return atlas_cell(split, coord)
}

FONT_SPLIT :: 16

@(require_results)
font_cell :: proc(coord: [2]i32) -> Rect {
    return atlas_cell(FONT_SPLIT, coord)
}

@(require_results)
font_slot :: proc(#any_int index: i32) -> Rect {
    return font_cell([2]i32{
        index % FONT_SPLIT,
        index / FONT_SPLIT,
    })
}

@(require_results)
hash_name :: #force_inline proc "contextless" (name: string) -> Hash {
    hash := hash_fnv64a(transmute([]byte)name, seed = HASH_SEED)
    return Hash(hash == 0 ? 1 : hash)
}

@(require_results)
hash_const_name :: #force_inline proc "contextless" ($Name: string) -> Hash {
    hash: u64 = #hash(Name, HASH_ALG)
    return Hash(hash == 0 ? 1 : hash)
}

@(require_results)
get_screen_size :: proc() -> [2]f32 {
    return {f32(_state.screen_size.x), f32(_state.screen_size.y)}
}

@(require_results)
get_viewport :: proc() -> [3]f32 {
    return {f32(_state.screen_size.x), f32(_state.screen_size.y), 1.0}
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Scene
//

load_scene :: proc(name: string, dst_group: Group_Handle) -> (result_group: Group_Handle, ok: bool) {
    bin_name := strings.concatenate({name, ".bin"}, context.temp_allocator)
    txt_data := get_file_by_name(name) or_return
    bin_data := get_file_by_name(bin_name) or_return

    return load_scene_from_data(string(txt_data), bin_data, dst_group)
}

load_scene_from_data :: proc(txt: string, bin: []byte, dst_group: Group_Handle) -> (Group_Handle, bool) {
    validate(len(txt) >= 5)
    validate(len(bin) >= 5)

    log.info("Loading Scene")

    parser := rscn.make_parser(txt)

    header, header_err := rscn.parse_header(&parser)
    if header_err != .OK {
        log.error("Failed to load scene: Header error")
        return {}, false
    }

    vert_buf := slice.reinterpret([]rscn.Mesh_Vertex, bin[header.mesh_vert_offs:])[:header.mesh_vert_num]
    index_buf := slice.reinterpret([]u16, bin[header.mesh_index_offs:])[:header.mesh_index_num]
    spline_vert_buf := slice.reinterpret([]rscn.Spline_Vertex, bin[header.spline_vert_offs:])[:header.spline_vert_num]

    group: ^Group
    group_handle: Group_Handle

    if dst_group != {} {
        ok: bool
        group, ok = get_group_state(dst_group)
        group_handle = dst_group

        if !ok {
            log.error("Failed to load scene: Invalid target group handle")
            return {}, false
        }

        // APPEND TO GPU

    } else {
        verts := make([]Mesh_Vertex, len(vert_buf), context.temp_allocator)
        for i in 0..<len(verts) {
            v := vert_buf[i]
            verts[i] = {
                pos = v.pos,
                uv = v.uv,
                normal = v.normal,
                color = v.color,
            }
        }

        ok: bool
        group_handle, ok = create_group(
            max_total_children  = i32(header.object_num),
            max_spline_verts    = i32(header.spline_vert_num),
            vertex_data         = verts,
            index_data          = index_buf,
        )

        if !ok {
            log.error("Failed to load scene: Couldn't create group")
            return {}, false
        }

        group, _ = get_group_state(group_handle)

        assert(group != nil)
    }

    object_list := make([]Object_Handle, header.object_num, context.temp_allocator)
    mesh_list := make([]Mesh_Handle, header.object_num, context.temp_allocator)
    spline_list := make([]Spline_Handle, header.object_num, context.temp_allocator)

    object_counter := 0
    mesh_counter := 0
    spline_counter := 0

    parse_loop: for {
        elem, elem_err := rscn.parse_next_elem(&parser)
        switch elem_err {
        case .OK:

        case .End:
            break parse_loop

        case .Error:
            log.error("Failed to parse scene file")
            break parse_loop
        }

        switch v in elem {
        case rscn.Comment:

        case rscn.Image:
            _, ok := get_texture_by_name(v.path)
            if ok {
                // Ignore existing textures.
                // Watcher should handle the data updates.
                continue
            }

            if !load_asset(v.path, {}) {
                log.error("Failed to load scene texture")
            }

        case rscn.Mesh:
            log.debug("Loading Mesh:", v.name)

            index := mesh_counter
            mesh_counter += 1

            mesh: Mesh
            mesh.group = group_handle

            mesh.vert_num = i32(v.vert_num)
            mesh.index_num = i32(v.index_num)
            mesh.vert_offs = i32(v.vert_start) + group.mesh_vert_num
            mesh.index_offs = i32(v.index_start) + group.mesh_index_num

            verts := vert_buf[v.vert_start:][:v.vert_num]
            // indexes := index_buf[v.index_start:][:v.index_num]

            mesh.bounds_min = max(f32)
            mesh.bounds_max = min(f32)
            for vert in verts {
                mesh.bounds_min = linalg.min(mesh.bounds_min, vert.pos)
                mesh.bounds_max = linalg.max(mesh.bounds_max, vert.pos)
            }

            handle, handle_ok := insert_mesh_by_name(v.name, mesh)
            if !handle_ok {
                log.error("Failed to insert mesh, table is full")
                return {}, false
            }

            mesh_list[index] = handle

        case rscn.Spline:
            log.debug("Loading Spline:", v.name)

            index := spline_counter
            spline_counter += 1

            spline: Spline
            spline.group = group_handle

            spline.vert_num = i32(v.vert_num)
            spline.vert_offs = group.spline_vert_num + i32(v.vert_start)

            verts := spline_vert_buf[v.vert_start:][:v.vert_num]

            if v.vert_num > (len(group.spline_vert_buf) - int(group.spline_vert_num)) {
                log.error("Failed to create spline, spline vertex buffer can't fit the data")
                continue
            }

            // NOTE: consider vert radius?
            spline.bounds_min = max(f32)
            spline.bounds_max = min(f32)
            for vert, i in verts {
                spline.bounds_min = linalg.min(spline.bounds_min, vert.pos)
                spline.bounds_max = linalg.max(spline.bounds_max, vert.pos)

                group.spline_vert_buf[group.spline_vert_num + i32(i)] = vert
            }

            handle, handle_ok := insert_spline_by_name(v.name, spline)
            if !handle_ok {
                log.error("Failed to insert spline, table is full")
                return {}, false
            }

            group.spline_vert_num += i32(len(verts))

            spline_list[index] = handle

        case rscn.Object:
            log.debug("Loading Object:", v.name)

            index := object_counter
            object_counter += 1

            object: Object
            object.group = group_handle

            object.kind = v.kind
            object.parent.index = v.parent == -1 ? HANDLE_INDEX_INVALID : Handle_Index(v.parent) // TEMP

            switch v.kind {
            case .Empty:
                object.data_handle = {}

            case .Mesh:
                object.data_handle.index = v.mesh_index == -1 ? HANDLE_INDEX_INVALID : Handle_Index(v.mesh_index) // TEMP

            case .Spline:
                object.data_handle.index = v.spline_index == -1 ? HANDLE_INDEX_INVALID : Handle_Index(v.spline_index) // TEMP
            }

            handle, handle_ok := insert_object_by_name(v.name, object)
            if !handle_ok {
                log.error("Failed to insert object, table is full")
                return {}, false
            }

            object_list[index] = handle
        }
    }

    // Resolve indices -> handles

    // NOTE: the 2nd pass might be unnecessary if the data is ordered the right way? enforce it in rscn?
    for handle in object_list {
        obj := get_internal_object(handle) or_continue

        if obj.parent.index == HANDLE_INDEX_INVALID {
            continue
        }

        obj.parent = object_list[obj.parent.index]

        if parent, parent_ok := get_internal_object(obj.parent); parent_ok {
            parent.child_num += 1
        }

        switch obj.kind {
        case .Empty:

        case .Mesh:
            obj.data_handle = Handle(mesh_list[obj.data_handle.index])

        case .Spline:
            obj.data_handle = Handle(spline_list[obj.data_handle.index])
        }
    }


    // Reserve child array space
    child_offset := group.object_child_num
    for handle in object_list {
        obj := get_internal_object(handle) or_continue

        obj.child_offset = child_offset

        if child_offset + obj.child_num > i32(len(group.object_child_buf)) {
            log.error("Group child buffer is too small to contain all children")
            obj.child_num = 0
            continue
        }

        child_offset += obj.child_num
    }

    group.object_child_num = child_offset

    // Fill child array
    for handle in object_list {
        obj := get_internal_object(handle) or_continue

        parent := get_internal_object(obj.parent) or_continue

        group.object_child_buf[parent.child_offset] = handle
        parent.child_offset += 1
    }

    // Reset child offsets (this is a bit weird, be careful)
    for handle in object_list {
        obj := get_internal_object(handle) or_continue
        obj.child_offset -= obj.child_num
    }

    return group_handle, true
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Input
//

MAX_GAMEPADS :: platform.MAX_GAMEPADS

Key :: platform.Key
Mouse_Button :: platform.Mouse_Button
Gamepad_Button :: platform.Gamepad_Button
Gamepad_Axis :: platform.Gamepad_Axis

Input :: struct {
    mouse_delta:        [2]f32,
    mouse_pos:          [2]f32,
    scroll_delta:       [2]f32,

    keys:               Input_Digital_Buffer(Key),
    mouse_buttons:      Input_Digital_Buffer(Mouse_Button),

    gamepads:           [MAX_GAMEPADS]Input_Gamepad,
    gamepads_connected: bit_set[0..<MAX_GAMEPADS],
}

Input_Gamepad :: struct {
    buttons:    Input_Digital_Buffer(Gamepad_Button),
    axes:       [Gamepad_Axis]f32,
}

Input_Digital_Buffer :: struct($E: typeid) where intrinsics.type_is_enum(E) {
    down:       bit_set[E],
    pressed:    bit_set[E],
    released:   bit_set[E],
    repeated:   bit_set[E],
    buffered:   bit_set[E],
    timer:      [E]f32,
}

_begin_input_digital_buffer_frame :: proc(buf: ^Input_Digital_Buffer($T), delta: f32) {
    buf.pressed = {}
    buf.repeated = {}
    buf.released = {}
    for &t in buf.timer {
        t += delta
    }
}

_input_digital_press :: proc(buf: ^Input_Digital_Buffer($T), elem: T) {
    if elem not_in buf.down {
        buf.pressed += {elem}
        buf.buffered += {elem}
        buf.timer[elem] = 0
    } else {
        buf.repeated += {elem}
    }
    buf.down += {elem}
}

_input_digital_release :: proc(buf: ^Input_Digital_Buffer($T), elem: T) {
    buf.down -= {elem}
    buf.released += {elem}
}

// NOTE: [0, 0] is the bottom left corner.
mouse_pos :: proc() -> [2]f32 {
    return _state.input.mouse_pos
}

// Positive Y is up.
mouse_delta :: proc() -> [2]f32 {
    return _state.input.mouse_delta
}

scroll_delta :: proc() -> [2]f32 {
    return _state.input.scroll_delta
}


key_down :: proc(key: Key) -> bool {
    return key in _state.input.keys.down
}

// Down time is 0 on pressed.
key_down_time :: proc(key: Key) -> f32 {
    return _state.input.keys.timer[key]
}

key_pressed :: proc(key: Key, buf: f32 = 0) -> bool {
    if buf > 0.0001 &&
        key in _state.input.keys.buffered &&
        _state.input.keys.timer[key] <= buf
    {
        _state.input.keys.buffered -= {key}
        return true
    }

    if key in _state.input.keys.pressed {
        return true
    }

    return false
}

key_repeated :: proc(key: Key) -> bool {
    return key in _state.input.keys.repeated
}

key_released :: proc(key: Key) -> bool {
    return key in _state.input.keys.released
}


mouse_down :: proc(button: Mouse_Button) -> bool {
    return button in _state.input.mouse_buttons.down
}

// Down time is 0 on pressed.
mouse_down_time :: proc(button: Mouse_Button) -> f32 {
    return _state.input.mouse_buttons.timer[button]
}

mouse_pressed :: proc(button: Mouse_Button, buf: f32 = 0) -> bool {
    if buf > 0.0001 &&
        button in _state.input.mouse_buttons.buffered &&
        _state.input.mouse_buttons.timer[button] <= buf
    {
        _state.input.mouse_buttons.buffered -= {button}
        return true
    }

    if button in _state.input.mouse_buttons.pressed {
        return true
    }

    return false
}

mouse_repeated :: proc(button: Mouse_Button) -> bool {
    return button in _state.input.mouse_buttons.repeated
}

mouse_released :: proc(button: Mouse_Button) -> bool {
    return button in _state.input.mouse_buttons.released
}




/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Lookups
//

get_children :: proc(handle: Object_Handle, loc := #caller_location) -> ([]Object_Handle, bool) #optional_ok {
    obj, obj_ok := get_internal_object(handle)
    if !obj_ok {
        log.error("Failed to get object's children: invalid handle", location = loc)
        return nil, false
    }

    group, group_ok := get_group_state(obj.group)
    if !group_ok {
        log.error("Failed to get object's children: object's group handle is invalid")
        return nil, false
    }

    return group.object_child_buf[obj.child_offset:][:obj.child_num], true
}

get_child_by_name :: proc(handle: Object_Handle, name: string) -> (result: Object_Handle, ok: bool) #optional_ok {
    children := get_children(handle) or_return

    hash := hash_name(name)

    for ch in children {
        if _state.objects_hash[ch.index] != hash {
            continue
        }

        if _state.objects_gen[ch.index] != ch.gen {
            continue
        }

        return ch, true
    }

    return {}, false
}


@(require_results)
get_mesh :: proc($Name: string) -> (result: Mesh_Handle, ok: bool) #optional_ok {
    return get_mesh_by_hash(hash_const_name(Name))
}

@(require_results)
get_mesh_by_name :: proc(name: string) -> (result: Mesh_Handle, ok: bool) #optional_ok {
    return get_mesh_by_hash(hash_name(name))
}

@(require_results)
get_mesh_by_hash :: proc(hash: Hash) -> (result: Mesh_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.meshes_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.meshes_gen[index],
    }, true
}


@(require_results)
get_object :: proc($Name: string) -> (result: Object_Handle, ok: bool) #optional_ok {
    return get_object_by_hash(hash_const_name(Name))
}

@(require_results)
get_object_by_name :: proc(name: string) -> (result: Object_Handle, ok: bool) #optional_ok {
    return get_object_by_hash(hash_name(name))
}

@(require_results)
get_object_by_hash :: proc(hash: Hash) -> (result: Object_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.objects_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.objects_gen[index],
    }, true
}



@(require_results)
get_texture :: proc($Name: string) -> (result: Texture_Handle, ok: bool) #optional_ok {
    return get_texture_by_hash(hash_const_name(Name))
}

@(require_results)
get_texture_by_name :: proc(name: string) -> (result: Texture_Handle, ok: bool) #optional_ok {
    return get_texture_by_hash(hash_name(name))
}

@(require_results)
get_texture_by_hash :: proc(hash: Hash) -> (result: Texture_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.textures_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.textures_gen[index],
    }, true
}



@(require_results)
get_spline :: proc($Name: string) -> (result: Spline_Handle, ok: bool) #optional_ok {
    return get_spline_by_hash(hash_const_name(Name))
}

@(require_results)
get_spline_by_name :: proc(name: string) -> (result: Spline_Handle, ok: bool) #optional_ok {
    return get_spline_by_hash(hash_name(name))
}

@(require_results)
get_spline_by_hash :: proc(hash: Hash) -> (result: Spline_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.splines_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.splines_gen[index],
    }, true
}



@(require_results)
get_vertex_shader :: proc($Name: string) -> (result: Vertex_Shader_Handle, ok: bool) #optional_ok {
    return get_vertex_shader_by_hash(hash_const_name(Name))
}

@(require_results)
get_vertex_shader_by_name :: proc(name: string) -> (result: Vertex_Shader_Handle, ok: bool) #optional_ok {
    return get_vertex_shader_by_hash(hash_name(name))
}

@(require_results)
get_vertex_shader_by_hash :: proc(hash: Hash) -> (result: Vertex_Shader_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.vertex_shaders_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.vertex_shaders_gen[index],
    }, true
}


@(require_results)
get_pixel_shader :: proc($Name: string) -> (result: Pixel_Shader_Handle, ok: bool) #optional_ok {
    return get_pixel_shader_by_hash(hash_const_name(Name))
}

@(require_results)
get_pixel_shader_by_name :: proc(name: string) -> (result: Pixel_Shader_Handle, ok: bool) #optional_ok {
    return get_pixel_shader_by_hash(hash_name(name))
}

@(require_results)
get_pixel_shader_by_hash :: proc(hash: Hash) -> (result: Pixel_Shader_Handle, ok: bool) #optional_ok {
    index := _table_lookup_hash(&_state.pixel_shaders_hash, hash) or_return
    return {
        index = Handle_Index(index),
        gen = _state.pixel_shaders_gen[index],
    }, true
}




@(require_results)
get_internal_draw_layer :: proc(index: i32) -> (result: ^Draw_Layer, ok: bool) {
    if index < 0 || index >= MAX_DRAW_LAYERS {
        return nil, false
    }
    return &_state.draw_layers[index], true
}

@(require_results)
get_internal_mesh :: proc(handle: Mesh_Handle) -> (result: ^Mesh, ok: bool) {
    return _table_get(&_state.meshes, _state.meshes_gen, handle)
}

@(require_results)
get_internal_object :: proc(handle: Object_Handle) -> (result: ^Object, ok: bool) {
    return _table_get(&_state.objects, _state.objects_gen, handle)
}

@(require_results)
get_internal_spline :: proc(handle: Spline_Handle) -> (result: ^Spline, ok: bool) {
    return _table_get(&_state.splines, _state.splines_gen, handle)
}

@(require_results)
get_internal_vertex_shader :: proc(handle: Vertex_Shader_Handle) -> (result: ^Vertex_Shader, ok: bool) {
    return _table_get(&_state.vertex_shaders, _state.vertex_shaders_gen, handle)
}

@(require_results)
get_internal_pixel_shader :: proc(handle: Pixel_Shader_Handle) -> (result: ^Pixel_Shader, ok: bool) {
    return _table_get(&_state.pixel_shaders, _state.pixel_shaders_gen, handle)
}




@(require_results)
insert_mesh_by_name :: proc(name: string, mesh: Mesh) -> (result: Mesh_Handle, ok: bool) {
    return insert_mesh_by_hash(hash_name(name), mesh)
}

@(require_results)
insert_object_by_name :: proc(name: string, object: Object) -> (result: Object_Handle, ok: bool) {
    return insert_object_by_hash(hash_name(name), object)
}

@(require_results)
insert_spline_by_name :: proc(name: string, spline: Spline) -> (result: Spline_Handle, ok: bool) {
    return insert_spline_by_hash(hash_name(name), spline)
}

@(require_results)
insert_vertex_shader_by_name :: proc(name: string, shader: Vertex_Shader) -> (result: Vertex_Shader_Handle, ok: bool) {
    return insert_vertex_shader_by_hash(hash_name(name), shader)
}

@(require_results)
insert_pixel_shader_by_name :: proc(name: string, shader: Pixel_Shader) -> (result: Pixel_Shader_Handle, ok: bool) {
    return insert_pixel_shader_by_hash(hash_name(name), shader)
}


@(require_results)
insert_mesh_by_hash :: proc(hash: Hash, mesh: Mesh) -> (result: Mesh_Handle, ok: bool) {
    index, _ := _table_insert_hash(&_state.meshes_hash, hash) or_return

    _state.meshes[index] = mesh

    result = {
        index = Handle_Index(index),
        gen = _state.meshes_gen[index],
    }

    return result, true
}

@(require_results)
insert_object_by_hash :: proc(hash: Hash, object: Object) -> (result: Object_Handle, ok: bool) {
    index, _ := _table_insert_hash(&_state.objects_hash, hash) or_return

    _state.objects[index] = object

    result = {
        index = Handle_Index(index),
        gen = _state.objects_gen[index],
    }

    return result, true
}

@(require_results)
insert_spline_by_hash :: proc(hash: Hash, spline: Spline) -> (result: Spline_Handle, ok: bool) {
    index, _ := _table_insert_hash(&_state.splines_hash, hash) or_return

    _state.splines[index] = spline

    result = {
        index = Handle_Index(index),
        gen = _state.splines_gen[index],
    }

    return result, true
}

@(require_results)
insert_vertex_shader_by_hash :: proc(hash: Hash, shader: Vertex_Shader) -> (result: Vertex_Shader_Handle, ok: bool) {
    index, _ := _table_insert_hash(&_state.vertex_shaders_hash, hash) or_return

    _state.vertex_shaders[index] = shader

    result = {
        index = Handle_Index(index),
        gen = _state.vertex_shaders_gen[index],
    }

    return result, true
}


@(require_results)
insert_pixel_shader_by_hash :: proc(hash: Hash, shader: Pixel_Shader) -> (result: Pixel_Shader_Handle, ok: bool) {
    index, _ := _table_insert_hash(&_state.pixel_shaders_hash, hash) or_return

    _state.pixel_shaders[index] = shader

    result = {
        index = Handle_Index(index),
        gen = _state.pixel_shaders_gen[index],
    }

    return result, true
}



@(require_results)
_table_insert_hash :: proc(table: ^[$N]Hash, hash: u64) -> (result: int, prev: Hash, ok: bool) {
    start_index := int(hash) %% N

    for offs in 0..<MAX_PROBE_DIST {
        index := (start_index + offs) %% N
        if index == 0 {
            continue
        }

        h := table[index]

        if h == 0 || h == hash {
            table[index] = hash
            return index, h, true
        }
    }

    return 0, 0, false
}

@(require_results)
_table_lookup_hash :: proc(table: ^[$N]Hash, hash: u64) -> (int, bool) {
    start_index := int(hash) %% N

    for offs in 0..<MAX_PROBE_DIST {
        index := (start_index + offs) %% N
        if table[index] == hash {
            return index, true
        }
    }

    return {}, false
}

@(require_results)
_table_get :: proc(table: ^[$N]$T, table_gen: [N]Handle_Gen, handle: $H/Handle) -> (^T, bool) #no_bounds_check {
    if handle.index <= 0 || handle.index >= N {
        return nil, false
    }

    if handle.gen != table_gen[handle.index] {
        return nil, false
    }

    return &table[handle.index], true
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Group
//

@(require_results)
get_group_state :: proc(handle: Group_Handle) -> (result: ^Group, ok: bool) {
    return _table_get(&_state.groups, _state.groups_gen, handle)
}

@(require_results)
create_group :: proc(
    max_mesh_verts:     i32 = 1024 * 16,
    max_mesh_indices:   i32 = 1024 * 32,
    max_spline_verts:   i32 = 1024,
    max_total_children: i32 = 1024,
    vertex_data:        []Mesh_Vertex = {},
    index_data:         []Vertex_Index = {},
) -> (result: Group_Handle, ok: bool) #optional_ok {
    used_set := (transmute(u64)_state.groups_used) | 1
    index := intrinsics.count_trailing_zeros(~used_set)
    if index == 64 {
        log.error("Failed to create group: There is already max number of groups")
        return {}, false
    }

    group := &_state.groups[index]

    _state.groups_used += {int(index)}

    group^ = Group{
        object_child_buf    = make([]Object_Handle, max_total_children, _state.allocator),
        spline_vert_buf     = make([]Spline_Vertex, max_spline_verts, _state.allocator),
    }

    // TODO: allow creating mutable groups with default data..?
    if vertex_data != nil {
        group.vbuf, ok = gpu.create_buffer("rv-group-vert-buf",
            stride  = size_of(Mesh_Vertex),
            // size    = size_of(Mesh_Vertex) * len(vertex_data),
            usage   = .Immutable,
            data    = gpu.slice_bytes(vertex_data),
        )
    } else {
        group.vbuf, ok = gpu.create_buffer("rv-group-vert-buf",
            stride  = size_of(Mesh_Vertex),
            size    = size_of(Mesh_Vertex) * max_mesh_verts,
            usage   = .Default,
        )
    }

    assert(ok)

    if index_data != nil {
        group.ibuf, ok = gpu.create_index_buffer("rv-group-index-buf",
            // size = size_of(Vertex_Index) * len(index_data),
            data = gpu.slice_bytes(index_data),
            usage = .Immutable,
        )
    } else {
        group.ibuf, ok = gpu.create_index_buffer("rv-group-index-buf",
            size = size_of(Vertex_Index) * max_mesh_indices,
            usage = .Default,
        )
    }

    assert(ok)

    handle := Group_Handle{
        index = Handle_Index(index),
        gen = _state.groups_gen[index],
    }

    return handle, true
}

clear_group :: proc(handle: Group_Handle) {
    group, group_ok := get_group_state(handle)
    if !group_ok {
        return
    }

    group.mesh_index_num = 0
    group.mesh_vert_num = 0
}

destroy_group :: proc(handle: Group_Handle) {
    group, group_ok := get_group_state(handle)
    if !group_ok {
        return
    }

    gpu.destroy_resource(group.vbuf)
    gpu.destroy_resource(group.ibuf)

    for i in 0..<MAX_MESHES {
        mesh := &_state.meshes[i]
        if mesh.group != handle {
            continue
        }

        mesh^ = {}
        _state.meshes_hash[i] = 0
        _state.meshes_gen[i] += 1
    }


    for i in 0..<MAX_OBJECTS {
        object := &_state.objects[i]
        if object.group != handle {
            continue
        }

        object^ = {}
        _state.objects_hash[i] = 0
        _state.objects_gen[i] += 1
    }

    for i in 0..<MAX_SPLINES {
        spline := &_state.splines[i]
        if spline.group != handle {
            continue
        }

        spline^ = {}
        _state.splines_hash[i] = 0
        _state.splines_gen[i] += 1
    }

    delete(group.spline_vert_buf, _state.allocator)
    delete(group.object_child_buf, _state.allocator)

    _state.groups[handle.index] = {}
    _state.groups_gen[handle.index] += 1
    _state.groups_used -= {int(handle.index)}
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Textures
//

// Texture pool allows for better batching when textures are the same size.
// When you create textures with the same size after this call they will get inserted into the pool.
// NOTE: Strongly prefer square and power-of-two sizes for texture pools.
// NOTE: A texture pool may not be destroyed.
// NOTE: Beware of the memory consumed by high-res texture pools!
create_texture_pool :: proc(size: IVec2, slices: i32) -> (ok: bool) {
    if _state.texture_pools_len >= len(_state.texture_pools) {
        log.error("Failed to create texture pool, too many texture pools")
        return false
    }

    pool: Texture_Pool
    pool.size = size
    pool.slices = slices
    pool.resource, ok = gpu.create_texture_2d("rv-tex-pool",
        format = .RGBA_U8_Norm,
        size = size,
        array_depth = slices,
    )

    assert(ok)

    if pool.resource == {} {
        log.errorf("Failed to create %ix%ix%i texture pool GPU resource", size.x, size.y, slices)
        return false
    }

    index := _state.texture_pools_len
    _state.texture_pools[index] = pool
    _state.texture_pools_len += 1

    return true
}

get_internal_texture :: proc(handle: Texture_Handle) -> (result: ^Texture, ok: bool) {
    return _table_get(&_state.textures, _state.textures_gen, handle)
}

create_texture_from_encoded_data :: proc(name: string, data: []byte) -> (result: Texture_Handle, ok: bool) {
    tex, tex_ok := decode_texture_data(data)
    if !tex_ok {
        log.errorf("Failed to decode texture '%s'", name)
    }

    result, ok = create_texture_from_data(name, tex)

    destroy_decoded_texture_data(&tex)

    return result, ok
}

create_texture_from_data :: proc(name: string, data: Texture_Data) -> (result: Texture_Handle, ok: bool) {
    assert(data.size.x > 0)
    assert(data.size.y > 0)
    assert(len(data.pixels) == int(data.size.x * data.size.y))

    hash := hash_name(name)

    index, prev := _table_insert_hash(&_state.textures_hash, hash) or_return

    texture := &_state.textures[index]

    create_resource := true

    for &pool, pool_index in _state.texture_pools[:_state.texture_pools_len] {
        if pool.size != data.size {
            continue
        }

        full_set := (u64(1) << u64(pool.slices)) - 1

        used_set := (transmute(u64)pool.slices_used)

        if full_set == used_set {
            log_internal("Pool {} is full", pool_index)
            continue
        }

        slice_index := intrinsics.count_trailing_zeros(~used_set)

        assert(slice_index < 64)

        log.infof("Creating a pooled texture '%s' of size %ix%i with index %i", name, data.size.x, data.size.y, index)

        create_resource = false

        pool.slices_used += {int(slice_index)}

        texture^ = Texture{
            size = {u16(data.size.x), u16(data.size.y)},
            pool_index = u8(pool_index),
            slice = u8(slice_index),
            resource = {},
        }

        gpu.update_texture_2d(
            pool.resource,
            gpu.slice_bytes(data.pixels),
            slice_index,
        )

        break
    }

    if create_resource {
        log.infof("Creating a non-pooled texture '%s' of size %ix%i with index %i", name, data.size.x, data.size.y, index)

        // Already exists, replace the old one.
        // Possibly a name hash collision.
        if prev == hash {
            gpu.destroy_resource(texture.resource)
            texture^ = {}
        }



        res, res_ok := gpu.create_texture_2d(strings.concatenate({"rv-tex-", name}, context.temp_allocator),
            format = .RGBA_U8_Norm,
            size = data.size,
            usage = .Immutable,
            data = gpu.slice_bytes(data.pixels),
        )

        assert(res_ok)

        texture^ = Texture{
            size = {u16(data.size.x), u16(data.size.y)},
            pool_index = max(u8),
            slice = 0,
            resource = res,
        }
    }

    result = {
        index = Handle_Index(index),
        gen = _state.textures_gen[index],
    }

    return result, true
}

destroy_texture :: proc(handle: Texture_Handle) {
    texture, texture_ok := get_internal_texture(handle)
    if !texture_ok {
        return
    }

    gpu.destroy_resource(texture.resource)
    texture^ = {}

    _state.textures_gen[handle.index] += 1
    _state.textures_hash[handle.index] = 0
}


@(require_results)
decode_texture_data :: proc(data: []byte) -> (result: Texture_Data, ok: bool) {
    size: [2]i32
    channels: i32

    stbi.set_flip_vertically_on_load(true)

    data := stbi.load_from_memory(
        buffer = raw_data(data),
        len = i32(len(data)),
        x = &size.x,
        y = &size.y,
        channels_in_file = &channels,
        desired_channels = 4,
    )

    if data == nil {
        log.errorf("Failed to decode texture: %s", stbi.failure_reason())
        return {}, false
    }

    result = {
        size = size,
        pixels = (cast([^][4]u8)data)[:size.x * size.y],
    }

    return result, true
}

// NOTE: this is potentially unsafe.
destroy_decoded_texture_data :: proc(data: ^Texture_Data) {
    stbi.image_free(raw_data(data.pixels))
    data^ = {}
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Shaders
//
// Two ways to create:
// - from source: run shaderprep with VFS includes. Primarily for development.
// - from native: load HLSL/GLSL or whatever directly. Primarily for pakfiles.
//


@(require_results)
create_vertex_shader :: proc(name: string, data: []byte) -> (result: Vertex_Shader_Handle, ok: bool) {
    shader: Vertex_Shader

    shader.shader, ok = gpu.create_shader(name, data, .Vertex)

    if !ok {
        log.error("RV: Failed to create vertex shader")
        return
    }

    // TODO: if this fails the shader gets leaked.
    // TODO: fix for ALL table inserts, including rscn loading and custom mesh creation etc.
    return insert_vertex_shader_by_name(name, shader)
}


@(require_results)
create_pixel_shader :: proc(name: string, data: []byte) -> (result: Pixel_Shader_Handle, ok: bool) {
    shader: Pixel_Shader

    shader.shader, ok = gpu.create_shader(name, data, .Pixel)

    if !ok {
        log.error("RV: Failed to create pixel shader")
        return
    }

    return insert_pixel_shader_by_name(name, shader)
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Files
//

// Registers all files.
// Loads all assets.
// TODO: flag to keep/flush the file data.
// TODO: flag to only register the file.
// TODO: file blacklist?
load_asset_directory :: proc(path: string, watch := false) {
    iter: platform.Directory_Iter

    if !platform.is_directory(path) {
        log.error("Cannot load data, '%s' is not a valid directory path", path)
    }

    pattern := strings.concatenate({path, "\\*"}, context.temp_allocator)

    files := make([dynamic]string, 0, 64, context.temp_allocator)

    for name in platform.iter_directory(&iter, pattern, context.temp_allocator) {
        full := filepath.join({path, name}, context.temp_allocator)

        if !platform.is_file(full) {
            continue
        }

        data, data_ok := platform.read_file_by_path(
            full,
            allocator = _state.allocator,
        )

        if !data_ok {
            log.errorf("Failed to load file '%s' from directory '%s'", name, path)
            continue
        }

        create_file_by_name(name, data, flags = {.Dynamically_Allocated})

        append(&files, name)
    }

    for file in files {
        load_asset(file, {})
    }

    watch_block: if watch {
        // Add dir to watched paths

        if _state.watched_dirs_num > MAX_WATCHED_DIRS {
            log.error("Failed to watch data directory, too many watched directories")
            return
        }

        index := _state.watched_dirs_num
        dir := &_state.watched_dirs[index]

        if !platform.init_file_watcher(&dir.watcher, path, recursive = false) {
            intrinsics.mem_zero(dir, size_of(Watched_Dir))
            break watch_block
        }

        dir.path_len = i32(copy(dir.path[:], path))

        _state.watched_dirs_num += 1
    }
}

load_constant_asset_directory :: proc(files: []runtime.Load_Directory_File) -> (all_ok: bool) {
    all_ok = true

    for file in files {
        log.info(file.name)
        if !create_file_by_name(file.name, file.data, flags = {}) {
            log.error("Failed to create file '%s' from a constant directory", file.name)
            continue
        }
    }

    for file in files {
        load_asset(file.name, {})
    }

    return all_ok
}

load_asset :: proc(name: string, dst_group: Group_Handle) -> bool {
    if strings.has_suffix(name, ".png") {
        data, data_ok := get_file_by_name(name)
        if !data_ok {
            log.errorf("Failed to load texture '%s', file not found", name)
            return false
        }
        _, ok := create_texture_from_encoded_data(name[:len(name) - 4], data)
        return ok
    } else if strings.has_suffix(name, ".rscn") {
        group_handle, ok := load_scene(name, dst_group = dst_group)
        return ok
    }
    // TODO
    // else if strings.has_suffix(name, ".wav") {
    // } else if strings.has_suffix(name, ".hlsl") {
    // }

    return true
}

get_file_by_name :: proc(name: string, flush := true) -> (data: []byte, ok: bool) {
    return get_file_by_hash(hash_name(name), flush = flush)
}

get_file_by_hash :: proc(hash: Hash, flush := true) -> (data: []byte, ok: bool) {
    index :=_table_lookup_hash(&_state.files_hash, hash) or_return

    file := &_state.files[index]

    if flush {
        if .Dirty in file.flags {
            file.flags -= {.Dirty}
            return file.data, true
        } else {
            return {}, false
        }
    }

    return file.data, true
}


create_file_by_name :: proc(name: string, data: []byte, flags: bit_set[File_Flag]) -> bool {
    log.infof("Creating file '%s' of size %M (%i bytes)", name, len(data), len(data))
    return create_file_by_hash(hash_name(name), data, flags)
}

create_file_by_hash :: proc(hash: Hash, data: []byte, flags: bit_set[File_Flag]) -> bool {
    index, _, ok := _table_insert_hash(&_state.files_hash, hash)
    if !ok {
        return false
    }

    _state.files[index] = File{
        data = data,
        flags = flags + {.Dirty},
    }

    return true
}

get_internal_file_by_hash :: proc(hash: Hash) -> (file: ^File, ok: bool) {
    index := _table_lookup_hash(&_state.files_hash, hash) or_return
    return &_state.files[index], true
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Render Texture
//

@(require_results)
create_render_texture :: proc(size: [2]i32, depth := true) -> (result: Render_Texture_Handle, ok: bool) {
    assert(size.x > 0)
    assert(size.y > 0)
    assert(size.x <= 4096) // arbitrary
    assert(size.y <= 4096)

    used_set := (transmute(u64)_state.render_textures_used) | 1
    index := intrinsics.count_trailing_zeros(~used_set)
    if index == 64 {
        log.error("Failed to create render texture: there is already max number of render textures")
        return {}, false
    }

    tex := &_state.render_textures[index]

    tex.color, ok = gpu.create_texture_2d("rv-render-tex",
        format = .RGBA_U8_Norm, // HDR option in the future?
        size = size,
        render_texture = true,
    )

    assert(ok)

    if tex.color != {} {
        log.error("Failed to create render texture color buffer")
        return {}, false
    }

    if depth {
        // WARNING: depth SRVs not yet implemented in gpu package
        tex.depth, ok = gpu.create_texture_2d("rv-depth-tex",
            format = .D_F32,
            size = size,
            render_texture = true,
        )

        assert(ok)

        if tex.depth == {} {
            log.error("Failed to create render texture depth buffer")
            return {}, false
        }
    }

    result = Render_Texture_Handle{
        index = Handle_Index(index),
        gen = _state.render_textures_gen[index],
    }

    _state.render_textures_used += {int(index)}

    return result, true
}

destroy_render_texture :: proc(handle: Render_Texture_Handle) {
    assert(handle.index != DEFAULT_RENDER_TEXTURE.index)
    tex, tex_ok := get_internal_render_texture(handle)
    if !tex_ok {
        // Completely fine, No-op
        return
    }

    _destroy_render_texture(tex)

    _state.render_textures_gen[handle.index] += 1
    _state.render_textures_used -= {int(handle.index)}
}

_destroy_render_texture :: proc(tex: ^Render_Texture) {
    gpu.destroy_resource(tex.color)
    gpu.destroy_resource(tex.depth)
    tex^ = {}
}

// resize_render_texture :: proc(handle: Render_Texture_Handle, size: [2]i32) {
//     assert(handle.index != DEFAULT_RENDER_TEXTURE.index)
//     _, tex_ok := get_internal_render_texture(handle)
//     if !tex_ok {
//         log.warn("Trying to resize invalid render texture")
//         return
//     }
// }

@(require_results)
get_internal_render_texture :: proc(handle: Render_Texture_Handle) -> (result: ^Render_Texture, ok: bool) {
    return _table_get(&_state.render_textures, _state.render_textures_gen, handle)
}

@(require_results)
get_render_texture_size :: proc(handle: Render_Texture_Handle) -> (result: [2]i32, ok: bool) {
    rt := get_internal_render_texture(handle) or_return
    return rt.size.xy, true
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Bind
//

@(deferred_none = pop_binds)
scope_binds :: proc() -> bool {
    push_binds()
    return true
}

push_binds :: proc() {
    if _state.bind_states_len >= MAX_BIND_STATE_DEPTH {
        log.error("Cannot set bind state, reached max depth")
        return
    }

    _state.bind_states[_state.bind_states_len] = _state.bind_state
    _state.bind_states_len += 1
}

pop_binds :: proc() {
    assert(_state.bind_states_len > 0)
    _state.bind_states_len -= 1
    _state.bind_state = _state.bind_states[_state.bind_states_len]
}

@(require_results)
get_binds :: proc() -> Bind_State {
    return _state.bind_state
}

// NOTE: be very careful when changing fields in Bind_State.
// This proc should be used mostly to revert state returned by 'get_binds'
set_binds :: proc(binds: Bind_State) {
    _state.bind_state = binds
}

bind_layer :: proc(#any_int layer: i32) {
    assert(layer >= 0 && layer <= MAX_DRAW_LAYERS)
    _state.bind_state.draw_layer = u8(layer)
}

bind_blend :: proc(blend: Blend_Mode) {
    _state.bind_state.blend = blend
}

bind_fill :: proc(fill: Fill_Mode) {
    _state.bind_state.fill = fill
}

bind_sprite_scaling :: proc(scaling: Sprite_Scaling) {
    _state.bind_state.sprite_scaling = scaling
}

bind_depth_write :: proc(write: bool) {
    _state.bind_state.depth_write = write
}

bind_depth_test :: proc(test: bool) {
    _state.bind_state.depth_test = test
}

bind_pixel_shader :: proc {
    bind_pixel_shader_by_name,
    bind_pixel_shader_by_handle,
}

bind_vertex_shader :: proc {
    bind_vertex_shader_by_name,
    bind_vertex_shader_by_handle,
}

bind_texture :: proc {
    bind_texture_by_const,
    bind_texture_by_name,
    bind_texture_by_handle,
    bind_render_texture_by_handle,
}


bind_pixel_shader_by_name :: proc(name: string) -> bool {
    bind_pixel_shader_by_handle(get_pixel_shader_by_name(name))
    return true
}

bind_vertex_shader_by_name :: proc(name: string) -> bool {
    bind_vertex_shader_by_handle(get_vertex_shader_by_name(name))
    return true
}

bind_texture_by_const :: proc($Name: string) -> bool {
    bind_texture_by_handle(get_texture_by_hash(hash_const_name(Name)))
    return true
}

bind_texture_by_name :: proc(name: string) -> bool {
    bind_texture_by_handle(get_texture_by_name(name))
    return true
}


bind_pixel_shader_by_handle :: proc(handle: Pixel_Shader_Handle) {
    if _, ok := get_internal_pixel_shader(handle); ok {
        _state.bind_state.pixel_shader = u8(handle.index)
    } else {
        _state.bind_state.pixel_shader = u8(_state.default_ps.index)
    }
}

bind_vertex_shader_by_handle :: proc(handle: Vertex_Shader_Handle) {
    if _, ok := get_internal_vertex_shader(handle); ok {
        _state.bind_state.vertex_shader = u8(handle.index)
    } else {
        _state.bind_state.vertex_shader = u8(_state.default_vs.index)
    }
}

bind_texture_by_handle :: proc(handle: Texture_Handle) {
    if !_bind_texture(handle) {
        _bind_texture(_state.error_texture)
    }
}

_bind_texture :: proc(handle: Texture_Handle) -> bool {
    tex := get_internal_texture(handle) or_return
    if tex.resource != {} {
        // Standalone tex
        _state.bind_state.texture_mode = .Non_Pooled
        _state.bind_state.texture = u8(handle.index)
        _state.bind_state.texture_slice = 0
        _state.bind_state.texture_size = tex.size
    } else {
        // Pool slice index
        pool := _state.texture_pools[tex.pool_index]
        assert(int(tex.slice) < int(pool.slices))
        assert(int(tex.slice) in pool.slices_used)

        _state.bind_state.texture_mode = .Pooled
        _state.bind_state.texture = u8(tex.pool_index)
        _state.bind_state.texture_slice = u8(tex.slice)
        _state.bind_state.texture_size = {
            u16(pool.size.x),
            u16(pool.size.y),
        }
    }
    return true
}

// Bind render texture for READING like a regular texture.
// In order to WRITE to a render texture, use layers.
bind_render_texture_by_handle :: proc(handle: Render_Texture_Handle) {
    assert(handle != DEFAULT_RENDER_TEXTURE)
    tex, tex_ok := get_internal_render_texture(handle)
    if !tex_ok {
        _bind_texture(_state.error_texture)
        return
    }
    assert(tex.color != {})

    _state.bind_state.texture_mode = .Render_Texture
    _state.bind_state.texture = u8(handle.index)
    _state.bind_state.texture_slice = 0
    _state.bind_state.texture_size = {
        u16(tex.size.x),
        u16(tex.size.y),
    }
}


// NOTE: Prefer calling this before any draw_* commands.
// But the params persist between frames.
set_layer_params :: proc(
    #any_int layer: i32,
    camera:         Camera,
    flags:          bit_set[Draw_Layer_Flag] = {},
) {
    layer, layer_ok := get_internal_draw_layer(layer)
    if !layer_ok {
        log.error("Invalid layer index")
        assert(false)
        return
    }

    layer.flags = flags
    layer.camera = camera
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Draw
//


// TODO: anchor
// TODO: sprite_ex
// TODO: scaling by pixels or absolute
// TODO: draw sprite line
draw_sprite :: proc(
    pos:    Vec3,
    rect:   Rect = {0, 1},
    scale:  Vec2 = 1,
    col:    Vec4 = 1,
    rot:    Quat = 1,
    anchor: Vec2 = 0,
    angle:  f32 = 0,
) {
    validate_vec(pos)
    validate_vec(scale)
    validate_quat(rot)

    UV_EPS :: (1.0 / 8192.0)

    if col.a < 0.01 || abs(scale.x * scale.y) < 0.0001 {
        return
    }

    mat := linalg.matrix3_from_quaternion_f32(rot *
        linalg.quaternion_angle_axis_f32(angle, {0, 0, 1}))

    rect_size := rect_full_size(rect)

    draw: Sprite_Draw

    size := Vec2{
        scale.x * 0.5,
        scale.y * 0.5,
    }

    switch _state.bind_state.sprite_scaling {
    case .Pixel:
        size *= {
            f32(_state.bind_state.texture_size.x) * rect_size.x,
            f32(_state.bind_state.texture_size.y) * rect_size.y,
        }

    case .Absolute:
        // No scaling
    }

    center := pos
    center += mat[0] * anchor.x * size.x
    center += mat[1] * anchor.y * size.y

    // TODO: flip texture *data* instead of flipping sprites?

    rect_size_sign := Vec2{
        math.sign_f32(rect_size.x),
        math.sign_f32(rect_size.y),
    }

    inst := Sprite_Inst{
        pos = center,
        mat_x = mat[0] * size.x,
        mat_y = mat[1] * size.y,
        uv_min_x = rect.min.x + rect_size_sign.x * UV_EPS,
        uv_min_y = rect.min.y + rect_size_sign.y * UV_EPS,
        uv_size = rect_size - rect_size_sign * UV_EPS * 2,
        color = {
            u8(clamp(col.r * 255, 0, 255)),
            u8(clamp(col.g * 255, 0, 255)),
            u8(clamp(col.b * 255, 0, 255)),
            u8(clamp(col.a * 255, 0, 255)),
        },
        tex_slice = _state.bind_state.texture_slice,
    }

    draw_sprite_inst(inst)
}

draw_sprite_inst :: proc(inst: Sprite_Inst) {
    draw: Sprite_Draw

    draw.inst = inst

    draw.key = Draw_Sort_Key{
        texture         = _state.bind_state.texture,
        texture_mode    = _state.bind_state.texture_mode,
        ps              = _state.bind_state.pixel_shader,
        vs              = u8(_state.default_sprite_vs.index), // for now the VS is fixed
        blend           = _state.bind_state.blend,
        fill            = _state.bind_state.fill,
        depth_test      = _state.bind_state.depth_test,
        depth_write     = _state.bind_state.depth_write,
        index_num       = 0,
        group           = 0,
        dist            = 0,
    }

    _push_sprite_draw(_state.bind_state.draw_layer, draw)
}


Draw_Text_Iter :: struct {
    // TODO
}

draw_text_iter :: proc() {

}

draw_text_next :: proc() -> (ok: bool) {
    return false
}

draw_text :: proc(
    text:       string, // UTF-8
    pos:        [3]f32,
    scale:      Vec2 = 1,
    anchor:     Vec2 = 0, // 0 = left aligned, 0.5 = centered, 1.0 = right aligned
    spacing:    Vec2 = 0, // x = character spacing, y = line spacing
    col:        Vec4 = 1,
    rot:        Quat = 1,
) {
    char_size := IVec2{
        i32(_state.bind_state.texture_size.x) / 16,
        i32(_state.bind_state.texture_size.y) / 16,
    }

    full_size := calc_text_size(
        text = text,
        scale = scale,
        char_size = char_size,
        spacing = spacing,
    )

    mat := linalg.matrix3_from_quaternion_f32(rot)

    center := pos - (mat[0] * full_size.x * anchor.x + mat[1] * full_size.y * anchor.y)

    offs: Vec2

    for r in text {
        if rune_is_drawable(r) {
            ch := rune_to_char(r)

            p := center + (
                mat[0] * offs.x +
                mat[1] * offs.y
            )

            draw_sprite(
                pos = p,
                rect = font_slot(ch),
                scale = scale,
                col = col,
                rot = rot,
            )
        }

        offs = text_glyph_apply(offs, r, scale = scale, char_size = char_size, spacing = spacing)
    }
}

rune_is_drawable :: proc(r: rune) -> bool {
    switch r {
    case ' ', '\n', '\t':
        return false
    }
    return true
}

calc_text_size :: proc(text: string, scale: Vec2, char_size: IVec2 = 8, spacing: Vec2 = 0) -> Vec2 {
    offs: Vec2

    size: Vec2

    for r in text {
        offs = text_glyph_apply(offs, r, scale = scale, char_size = char_size, spacing = spacing)
        size = {
            max(size.x, offs.x),
            max(size.y, offs.y),
        }
    }

    return size + {0, (f32(char_size.y) + spacing.y) * scale.y}
}

text_glyph_apply :: proc(offs: Vec2, r: rune, scale: Vec2, char_size: IVec2 = 8, spacing: Vec2 = 0) -> Vec2 {
    offs := offs

    switch r {
    case '\n':
        offs.x = 0
        offs.y -= scale.y * (f32(char_size.y) + spacing.y)
        return offs

    case '\t':
        tab_size := scale.x * (f32(char_size.x) + spacing.x) * 4
        offs.x = math.ceil_f32(offs.x / tab_size + 1) * tab_size
        return offs
    }

    offs.x += scale.x * (f32(char_size.x) + spacing.x)

    return offs
}

draw_mesh_by_handle :: proc(
    handle:     Mesh_Handle,
    pos:        Vec3,
    rot:        Quat = 1,
    scale:      Vec3 = 1,
    col:        Vec4 = 1,
    param:      u32 = 0,
) {
    validate_vec(pos)
    validate_vec(scale)
    validate_quat(rot)

    mesh, mesh_ok := get_internal_mesh(handle)
    if !mesh_ok {
        // TODO: draw "error mesh" instead
        log.error("Trying to draw a mesh with invalid handle")
        return
    }

    draw: Mesh_Draw

    mat := linalg.matrix3_from_quaternion_f32(rot)

    draw.key = {
        index_num       = u16(mesh.index_num),
        index_offs      = u32(mesh.index_offs),
        group           = u8(mesh.group.index),
        texture         = _state.bind_state.texture,
        texture_mode    = _state.bind_state.texture_mode,
        ps              = _state.bind_state.pixel_shader,
        vs              = _state.bind_state.vertex_shader,
        fill            = _state.bind_state.fill,
        blend           = _state.bind_state.blend,
        depth_test      = _state.bind_state.depth_test,
        depth_write     = _state.bind_state.depth_write,
    }

    if linalg.matrix3x3_determinant(mat) < 0 {
        switch draw.key.fill {
        case .All, .Wire: // no op
        case .Front: draw.key.fill = .Back
        case .Back: draw.key.fill = .Front
        }
    }

    draw.inst = {
        pos = pos,
        mat_x = mat[0] * scale.x,
        mat_y = mat[1] * scale.y,
        mat_z = mat[2] * scale.z,
        col = {
            u8(clamp(col.r * 255, 0, 255)),
            u8(clamp(col.g * 255, 0, 255)),
            u8(clamp(col.b * 255, 0, 255)),
            u8(clamp(col.a * 255, 0, 255)),
        },
        tex_slice = _state.bind_state.texture_slice,
        vert_offs = u32(mesh.vert_offs),
        _pad0 = 0,
        param = param,
    }

    draw.extra = {
        index = handle.index,
    }

    _push_mesh_draw(_state.bind_state.draw_layer, draw)
}

draw_sprite_line :: proc(
    a:          Vec3,
    b:          Vec3,
    width:      f32,
    rect:       Rect = {0, 1},
    col:        Vec4 = 1,
) {
    draw_layer := &_state.draw_layers[_state.bind_state.draw_layer]

    mid := (a + b) * 0.5
    dir := linalg.normalize(b - a)
    // TODO: 2d and 3d might need a little different code path?
    // forw := linalg.normalize0(mid - draw_layer.camera.pos)
    forw := linalg.quaternion128_mul_vector3(draw_layer.camera.rot, Vec3{0, 0, 1})
    right := linalg.normalize0(linalg.cross(dir, forw))
    dist := linalg.distance(a, b)

    draw: Sprite_Draw

    // TODO: flip texture *data* instead of flipping sprites?

    UV_EPS :: (1.0 / 8192.0)

    draw.inst = Sprite_Inst{
        pos = mid,
        mat_x = right * width,
        mat_y = dir * dist * 0.5,
        uv_min_x = rect.min.x + UV_EPS,
        uv_min_y = rect.min.y + UV_EPS,
        uv_size = rect_full_size(rect) - UV_EPS * 2,
        color = {
            u8(clamp(col.r * 255, 0, 255)),
            u8(clamp(col.g * 255, 0, 255)),
            u8(clamp(col.b * 255, 0, 255)),
            u8(clamp(col.a * 255, 0, 255)),
        },
        tex_slice = _state.bind_state.texture_slice,
    }

    draw.key = Draw_Sort_Key{
        texture         = _state.bind_state.texture,
        texture_mode    = _state.bind_state.texture_mode,
        ps              = _state.bind_state.pixel_shader,
        vs              = u8(_state.default_sprite_vs.index), // for now the VS is fixed
        blend           = _state.bind_state.blend,
        fill            = _state.bind_state.fill,
        depth_test      = _state.bind_state.depth_test,
        depth_write     = _state.bind_state.depth_write,
        index_num       = 0,
        group           = 0,
        dist            = 0,
    }

    _push_sprite_draw(_state.bind_state.draw_layer, draw)
}

draw_triangle :: proc(
    pos:        [3]Vec3,
    uvs:        [3]Vec2 = {{0, 0}, {1, 0}, {0, 1}},
    col:        [3]Vec3 = {{1, 1, 1}, {1, 1, 1}, {1, 1, 1}},
    normals:    Maybe([3]Vec3) = nil,
) {
    validate_vec(pos[0])
    validate_vec(pos[1])
    validate_vec(pos[2])

    draw: Triangle_Draw

    draw.key = {
        index_num       = 0,
        group           = 0,
        texture         = _state.bind_state.texture,
        texture_mode    = _state.bind_state.texture_mode,
        ps              = _state.bind_state.pixel_shader,
        vs              = _state.bind_state.vertex_shader,
        fill            = _state.bind_state.fill,
        blend           = _state.bind_state.blend,
        depth_test      = _state.bind_state.depth_test,
        depth_write     = _state.bind_state.depth_write,
    }

    norm, norm_ok := normals.?
    if !norm_ok {
        norm = linalg.normalize0(linalg.cross(pos[1] - pos[0], pos[2] - pos[0]))
    }

    for i in 0..<3 {
        draw.verts[i] = {
            pos     = pos[i],
            uv      = uvs[i],
            normal  = {
                u8(clamp((norm[i].x * 0.5 + 0.5) * 255, 0, 255)),
                u8(clamp((norm[i].y * 0.5 + 0.5) * 255, 0, 255)),
                u8(clamp((norm[i].z * 0.5 + 0.5) * 255, 0, 255)),
            },
            p0      = 0,
            color   = {
                u8(clamp(col[i].r * 255, 0, 255)),
                u8(clamp(col[i].g * 255, 0, 255)),
                u8(clamp(col[i].b * 255, 0, 255)),
            },
            p1      = 0,
        }
    }

    _push_triangle_draw(_state.bind_state.draw_layer, draw)
}



// TODO: multi push?

_push_sprite_draw :: proc(#any_int layer_index: int, draw: Sprite_Draw) {
    draw_layer := &_state.draw_layers[layer_index]

    if len(draw_layer.sprites) == 0 {
        draw_layer.sprites = make_soa_dynamic_array_len_cap(#soa[dynamic]Sprite_Draw, 0, max(256, draw_layer.last_sprites_len), context.temp_allocator)
    }

    assert(draw_layer.sprites.allocator == context.temp_allocator)
    non_zero_append_soa_elem(&draw_layer.sprites, draw)
}

_push_mesh_draw :: proc(#any_int layer_index: int, draw: Mesh_Draw) {
    draw_layer := &_state.draw_layers[layer_index]

    if len(draw_layer.meshes) == 0 {
        draw_layer.meshes = make_soa_dynamic_array_len_cap(#soa[dynamic]Mesh_Draw, 0, max(256, draw_layer.last_meshes_len), context.temp_allocator)
    }

    assert(draw_layer.meshes.allocator == context.temp_allocator)
    non_zero_append_soa_elem(&draw_layer.meshes, draw)
}

_push_triangle_draw :: proc(#any_int layer_index: int, draw: Triangle_Draw) {
    draw_layer := &_state.draw_layers[layer_index]

    if len(draw_layer.triangles) == 0 {
        draw_layer.triangles = make_soa_dynamic_array_len_cap(#soa[dynamic]Triangle_Draw, 0, max(256, draw_layer.last_triangles_len), context.temp_allocator)
    }

    assert(draw_layer.triangles.allocator == context.temp_allocator)
    non_zero_append_soa_elem(&draw_layer.triangles, draw)
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: GPU Data Upload
//

_upload_gpu_global_constants :: proc() {
    gpu.update_constants(_state.global_consts, gpu.ptr_bytes(&Draw_Global_Constants{
        time = get_time(),
        delta_time = get_delta_time(),
        frame = u32(get_frame_index()),
        resolution = _state.screen_size,
        rand_seed = 0,
        param0 = 0,
        param1 = 0,
        param2 = 0,
        param3 = 0,
    }))
}

_upload_gpu_layer_constants :: proc() {
    consts_buf: [MAX_DRAW_LAYERS]Draw_Layer_Constants

    for &layer, i in _state.draw_layers {
        if len(layer.sprites) == 0 && len(layer.meshes) == 0 {
            continue
        }

        const_data: Draw_Layer_Constants = {
            view_proj = calc_camera_world_to_clip_matrix(layer.camera),
            cam_pos = layer.camera.pos,
            layer_index = i32(i),
        }

        consts_buf[i] = const_data
    }

    gpu.update_constants(_state.draw_layers_consts, gpu.ptr_bytes(&consts_buf))
}

// This takes all the draw_* command data and uploads them to GPU buffers.
// Call render_gpu_layer(...) to actually draw.
// NOTE: until the start of the next frame, all draw_* commands after this call will be ignored.
@(optimization_mode="favor_size")
upload_gpu_layers :: proc() {
    assert(!_state.uploaded_gpu_draws)
    _state.uploaded_gpu_draws = true

    _upload_gpu_global_constants()

    _upload_gpu_layer_constants()


    Batcher_State :: struct {
        consts:         [MAX_TOTAL_DRAW_BATCHES]Draw_Batch_Constants,
        consts_num:     u32,
    }

    batcher: Batcher_State

    for layer in _state.draw_layers {
        _counter_add(.Num_Total_Instances, u64(
            len(layer.sprites) +
            len(layer.meshes) +
            len(layer.triangles),
        ))
    }

    // Prepare sprites

    total_sprite_instances := 0

    for &layer, _ in _state.draw_layers {
        if len(layer.sprites) == 0 {
            continue
        }

        if .No_Cull not_in layer.flags {
            frustum := calc_camera_frustum(layer.camera)

            far_plane := frustum.planes[FRUSTUM_FAR_PLANE_INDEX]

            sprite_dist_factor := f32(MAX_DRAW_SORT_KEY_DIST) / far_plane.w

            forw := linalg.quaternion128_mul_vector3(layer.camera.rot, Vec3{0, 0, 1})

            for sprite_index := len(layer.sprites) - 1; sprite_index >= 0; sprite_index -= 1 {
                inst := layer.sprites.inst[sprite_index]
                key := &layer.sprites.key[sprite_index]

                bounds_rad :=
                    linalg.abs(inst.mat_x) +
                    linalg.abs(inst.mat_y)

                if key.blend != .Opaque {
                    dist := linalg.dot(forw, inst.pos - layer.camera.pos)
                    key.dist = ~u16(dist * sprite_dist_factor)
                }

                if !is_box_in_frustum(frustum, inst.pos, bounds_rad) {
                    unordered_remove_soa(&layer.sprites, sprite_index)
                }
            }
        }

        instances := layer.sprites.inst[:len(layer.sprites)]

        if .No_Reorder not_in layer.flags {
            keys := layer.sprites.key[:len(layer.sprites)]

            #assert(size_of(Draw_Sort_Key) == size_of(u128))
            indices := slice.sort_with_indices(transmute([]u128)keys, context.temp_allocator)
            slice.sort_from_permutation_indices(instances, indices)
        }

        total_sprite_instances += len(layer.sprites)
    }

    // GPU Upload sprites

    sprite_upload_buf, sprite_upload_err := runtime.mem_alloc_non_zeroed(size_of(Sprite_Inst) * total_sprite_instances, alignment = 256, allocator = context.temp_allocator)
    sprite_upload_offs := 0

    assert(sprite_upload_err == nil)

    for &layer, _ in _state.draw_layers {
        if len(layer.sprites) == 0 {
            continue
        }

        assert(sprite_upload_offs < len(sprite_upload_buf))

        instances := layer.sprites.inst[:len(layer.sprites)]

        uploaded_bytes := copy_slice(sprite_upload_buf[sprite_upload_offs:], gpu.slice_bytes(instances))
        total_bytes := size_of(Sprite_Inst) * len(layer.sprites)

        layer.sprite_insts_base = u32(sprite_upload_offs) / size_of(Sprite_Inst)

        if uploaded_bytes != total_bytes {
            log.error("Failed to upload all sprite instances")
            footer := raw_soa_footer_dynamic_array(&layer.sprites)
            footer.len = uploaded_bytes / size_of(Sprite_Inst)
        }
        sprite_upload_offs += uploaded_bytes
    }

    gpu.update_buffer(_state.sprite_inst_buf, sprite_upload_buf)

    // Generate sprite draw call lists

    for &layer, _ in _state.draw_layers {
        _batcher_generate_draws(&batcher,
            &layer.sprite_batches,
            layer.sprites.key[:len(layer.sprites)],
            layer.sprite_insts_base,
        )
        if len(layer.sprites) > 0 {
            assert(len(layer.sprite_batches) > 0)
        }
    }



    //
    // Upload meshes
    //

    total_mesh_instances := 1 // index 0 is dummy instance

    for &layer, _ in _state.draw_layers {
        if len(layer.meshes) == 0 {
            continue
        }

        if .No_Cull not_in layer.flags {
            MAX_MESH_DIST :: (1 << 16) - 1

            frustum := calc_camera_frustum(layer.camera)
            far_plane := frustum.planes[FRUSTUM_FAR_PLANE_INDEX]
            mesh_dist_factor := f32(MAX_MESH_DIST) / far_plane.w
            forw := linalg.quaternion128_mul_vector3(layer.camera.rot, Vec3{0, 0, 1})

            for mesh_index := len(layer.meshes) - 1; mesh_index >= 0; mesh_index -= 1 {
                inst := layer.meshes.inst[mesh_index]
                key := &layer.meshes.key[mesh_index]
                extra := layer.meshes.extra[mesh_index]

                mesh := _state.meshes[extra.index]

                box_rad :=
                    (linalg.abs(inst.mat_x) * max(abs(mesh.bounds_min.x), abs(mesh.bounds_max.x))) +
                    (linalg.abs(inst.mat_y) * max(abs(mesh.bounds_min.y), abs(mesh.bounds_max.y))) +
                    (linalg.abs(inst.mat_z) * max(abs(mesh.bounds_min.z), abs(mesh.bounds_max.z)))

                if key.blend != .Opaque || true {
                    dist := linalg.dot(forw, inst.pos - layer.camera.pos)
                    // NOTE: should this get inverted for opaque meshes to minimize overdraw?
                    // What about Z prepass?
                    key.dist = ~u16(dist * mesh_dist_factor) // invert
                }

                if !is_box_in_frustum(frustum, inst.pos, box_rad) {
                    unordered_remove_soa(&layer.meshes, mesh_index)
                }
            }
        }

        instances := layer.meshes.inst[:len(layer.meshes)]

        if .No_Reorder not_in layer.flags {
            keys := layer.meshes.key[:len(layer.meshes)]
            #assert(size_of(Draw_Sort_Key) == size_of(u128))
            indices := slice.sort_with_indices(transmute([]u128)keys, context.temp_allocator)
            slice.sort_from_permutation_indices(instances, indices)
        }

        total_mesh_instances += len(layer.meshes)
    }

    mesh_upload_buf := slice.reinterpret([]Mesh_Inst,
        runtime.mem_alloc_non_zeroed(size_of(Mesh_Inst) * total_mesh_instances, alignment = 256, allocator = context.temp_allocator) or_else panic("Mesh Buf"),
    )
    mesh_upload_offs := 0

    mesh_upload_offs += 1
    mesh_upload_buf[0] = Mesh_Inst{
        pos         = {0, 0, 0},
        mat_x       = {1, 0, 0},
        mat_y       = {0, 1, 0},
        mat_z       = {0, 0, 1},
        col         = 255,
        vert_offs   = 0,
        tex_slice   = 0, // OOPS
        _pad0       = 0,
        param       = 0,
    }

    for &layer, _ in _state.draw_layers {
        if len(layer.meshes) == 0 {
            continue
        }

        assert(mesh_upload_offs < len(mesh_upload_buf))

        instances := layer.meshes.inst[:len(layer.meshes)]

        uploaded_num := copy_slice(mesh_upload_buf[mesh_upload_offs:], instances)

        layer.mesh_insts_base = u32(mesh_upload_offs)

        if uploaded_num != len(layer.meshes) {
            log.error("Failed to upload all mesh instances")
            footer := raw_soa_footer_dynamic_array(&layer.meshes)
            footer.len = uploaded_num
        }

        mesh_upload_offs += uploaded_num
    }

    gpu.update_buffer(
        _state.mesh_inst_buf,
        gpu.slice_bytes(mesh_upload_buf[:mesh_upload_offs]),
    )

    for &layer, _ in _state.draw_layers {
        _batcher_generate_draws(&batcher,
            &layer.mesh_batches,
            layer.meshes.key[:len(layer.meshes)],
            layer.mesh_insts_base,
        )
        if len(layer.meshes) > 0 {
            assert(len(layer.mesh_batches) > 0)
        }
    }



    //
    // Upload triangles
    //

    total_triangle_instances := 0

    for &layer, _ in _state.draw_layers {
        if len(layer.triangles) == 0 {
            continue
        }

        total_triangle_instances += len(layer.triangles)
    }


    triangle_upload_buf, triangle_upload_err := runtime.mem_alloc_non_zeroed(size_of([3]Mesh_Vertex) * total_triangle_instances, alignment = 256, allocator = context.temp_allocator)
    triangle_upload_offs := 0

    assert(triangle_upload_err == nil)

    for &layer in _state.draw_layers {
        if len(layer.triangles) == 0 {
            continue
        }

        assert(triangle_upload_offs < len(triangle_upload_buf))

        data := layer.triangles.verts[:len(layer.triangles)]

        uploaded_bytes := copy_slice(triangle_upload_buf[triangle_upload_offs:], gpu.slice_bytes(data))
        total_bytes := size_of([3]Mesh_Vertex) * len(layer.triangles)

        layer.triangle_insts_base = u32(triangle_upload_offs) / size_of([3]Mesh_Vertex)

        if uploaded_bytes != total_bytes {
            log.error("Failed to upload all triangles")
            footer := raw_soa_footer_dynamic_array(&layer.triangles)
            footer.len = uploaded_bytes / size_of([3]Mesh_Vertex)
        }
        triangle_upload_offs += uploaded_bytes
    }

    gpu.update_buffer(_state.triangle_vbuf, triangle_upload_buf)

    // Generate triangle draws

    for &layer, _ in _state.draw_layers {
        _batcher_generate_draws(&batcher,
            &layer.triangle_batches,
            layer.triangles.key[:len(layer.triangles)],
            layer.triangle_insts_base,
        )
        if len(layer.triangles) > 0 {
            assert(len(layer.triangle_batches) > 0)
        }
    }


    // Upload actual batch consts for all draws

    gpu.update_constants(
        _state.draw_batch_consts,
        gpu.slice_bytes(batcher.consts[:batcher.consts_num]),
    )


    for layer in _state.draw_layers {
        _counter_add(.Num_Uploaded_Instances, u64(
            len(layer.sprites) +
            len(layer.meshes) +
            len(layer.triangles),
        ))
    }

    for &layer, _ in _state.draw_layers {
        for b in layer.sprite_batches {
            validate_draw_sort_key(b.key)
        }

        for b in layer.mesh_batches {
            validate_draw_sort_key(b.key)
        }

        for b in layer.triangle_batches {
            validate_draw_sort_key(b.key)
        }
    }


    return

    _batcher_generate_draws :: proc(
        batcher:        ^Batcher_State,
        dst_batches:    ^[dynamic]Draw_Batch,
        keys:           []Draw_Sort_Key,
        inst_offs_base: u32,
    ) {
        if len(keys) == 0 {
            return
        }

        assert(dst_batches^ == nil)
        dst_batches^ = make([dynamic]Draw_Batch, 0, 256, context.temp_allocator)

        curr_key := keys[0]

        last := len(keys)

        instance_num: u32 = 0
        instance_offs: u32 = 0

        for i := 1; i <= last; i += 1 {
            instance_num += 1

            layer_key: Draw_Sort_Key
            if i != last {
                layer_key = keys[i]

                if draw_sort_key_equal(curr_key, layer_key) {
                    continue
                }
            }

            validate_draw_sort_key(curr_key)

            batch := Draw_Batch{
                key = curr_key,
                offset = u32(batcher.consts_num),
                num = int_cast(u16, instance_num),
            }

            append_elem(dst_batches, batch)

            assert(batcher.consts_num < len(batcher.consts))
            batcher.consts[batcher.consts_num] = {
                instance_offset = inst_offs_base + instance_offs,
            }
            batcher.consts_num += 1

            instance_offs += instance_num
            instance_num = 0
            curr_key = layer_key
        }
    }
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: GPU Drawing
//

// NOTE: the instance bind data only use a few of the available sots (consts/resources/blends/etc)
// We could possibly expose a direct way for the user to control this on per-layer basis.
// Custom pipeline and pass desc input?
@(optimization_mode="favor_size")
render_gpu_layer :: proc(
    #any_int index: i32,
    ren_tex_handle: Render_Texture_Handle,
    clear_color:    Maybe(Vec3),
    clear_depth:    bool,
) {
    assert(ren_tex_handle != {})
    assert(_state.uploaded_gpu_draws, "You must call upload_gpu_layers() to submit draw data to VRAM before any actual rendering")

    layer := _state.draw_layers[index]

    ren_tex, ren_tex_ok := get_internal_render_texture(ren_tex_handle)
    if !ren_tex_ok {
        log.error("Trying to submit GPU commands of an invalid render texture:", ren_tex_handle)
        return
    }

    clear_color_val: [4]f32 = {0, 0, 0, 1}
    clear_color_val.rgb = clear_color.? or_else {}
    pass_desc := gpu.Pass_Desc{
        colors = {
            0 = {
                resource = ren_tex.color,
                clear_mode = clear_color == nil ? .Keep : .Clear,
                clear_val = clear_color_val,
            },
        },
        depth = {
            resource = ren_tex.depth,
            clear_mode = clear_depth ? .Clear : .Keep,
            clear_val = 0.0,
        },
    }

    gpu.begin_pass(pass_desc)

    // BIG WARNING:
    // On certain GPU backends, the pipeline state has to be baked and a new pipeline has to be created,
    // when it's not already in pipeline cache.
    // For this reason a lot of care should be taken to minimize possible states.
    pip_desc := gpu.Pipeline_Desc {
        color_format = {
            0 = .RGBA_U8_Norm,
        },
        depth_format = .D_F32,
        topo = .Triangles,
        constants = {
            0 = _state.global_consts,
            1 = _state.draw_layers_consts,
            2 = _state.draw_batch_consts,
        },
    }

    for smp, i in DEFAULT_SAMPLERS {
        pip_desc.samplers[i] = smp
    }


    //
    // Sprites
    //

    pip_desc.index = {
        resource = _state.quad_ibuf,
        format = .U16,
    }

    pip_desc.resources = {
        0 = _state.sprite_inst_buf,
    }

    for batch in layer.sprite_batches {
        // log_internal("Sprite batch drawcall with %i instances", batch.num)

        _set_pipeline_desc_apply_key(&pip_desc, batch.key)

        pipeline, pipeline_ok := gpu.create_pipeline("sprite-pip", pip_desc)
        if !pipeline_ok {
            log.error("Failed to create GPU pipeline")
            continue
        }

        gpu.begin_pipeline(pipeline)

        _counter_add(.Num_Draw_Calls, 1)

        gpu.draw_indexed(
            index_num = 6,
            instance_num = batch.num,
            index_offset = 0,
            const_offsets = {
                0 = max(u32),
                1 = u32(index),
                2 = batch.offset,
            },
        )
    }


    //
    // Meshes
    //

    pip_desc.index = {
        resource = {},
        format = .U16,
    }

    pip_desc.resources = {
        0 = _state.mesh_inst_buf,
        1 = {},
    }

    for batch in layer.mesh_batches {
        _set_pipeline_desc_apply_key(&pip_desc, batch.key)

        pip_desc.index.resource = _state.groups[batch.key.group].ibuf
        pip_desc.resources[1] = _state.groups[batch.key.group].vbuf

        pipeline, pipeline_ok := gpu.create_pipeline("mesh-pip", pip_desc)
        if !pipeline_ok {
            log.error("Failed to create GPU pipeline")
            continue
        }

        gpu.begin_pipeline(pipeline)

        _counter_add(.Num_Draw_Calls, 1)

        gpu.draw_indexed(
            index_num = batch.key.index_num,
            instance_num = batch.num,
            index_offset = batch.key.index_offs,
            const_offsets = {
                0 = max(u32),
                1 = u32(index),
                2 = batch.offset,
            },
        )
    }


    //
    // Triangles
    //

    pip_desc.index = {
        resource = {},
        format = .U16,
    }

    pip_desc.index = {}
    pip_desc.resources = {
        0 = _state.mesh_inst_buf, // Only uses dummy 0 index. HACK: texture layers don't work
        1 = _state.triangle_vbuf,
    }

    for batch in layer.triangle_batches {
        _set_pipeline_desc_apply_key(&pip_desc, batch.key)

        pipeline, pipeline_ok := gpu.create_pipeline("tri-pip", pip_desc)
        if !pipeline_ok {
            log.error("Failed to create GPU pipeline")
            continue
        }

        gpu.begin_pipeline(pipeline)

        _counter_add(.Num_Draw_Calls, 1)

        gpu.draw_non_indexed(
            vertex_num = batch.num * 3,
            instance_num = 1,
            const_offsets = {
                0 = max(u32),
                1 = u32(index),
                2 = batch.offset,
            },
        )
    }

    return

    _set_pipeline_desc_apply_key :: proc(pip_desc: ^gpu.Pipeline_Desc, key: Draw_Sort_Key, loc := #caller_location) {
        validate_draw_sort_key(key)

        pip_desc.blends[0] = _gpu_blend_mode_desc(key.blend)
        pip_desc.cull, pip_desc.fill = _gpu_fill_mode(key.fill)
        pip_desc.depth_comparison = key.depth_test ? .Greater_Equal : .Always
        pip_desc.depth_write = key.depth_write
        pip_desc.ps = _state.pixel_shaders[key.ps].shader
        pip_desc.vs = _state.vertex_shaders[key.vs].shader

        tex_res: gpu.Resource_Handle
        switch key.texture_mode {
        case .Non_Pooled:       tex_res = _state.textures[key.texture].resource
        case .Pooled:           tex_res = _state.texture_pools[key.texture].resource
        case .Render_Texture:   tex_res = _state.render_textures[key.texture].color
        case: panic("Invalid texture mode")
        }

        assert(tex_res != {}, "Invalid texture resource", loc = loc)
        pip_desc.resources[2] = tex_res
    }
}


_gpu_blend_mode_desc :: proc(blend: Blend_Mode) -> gpu.Blend_Desc {
    switch blend {
    case .Opaque:
        return gpu.BLEND_OPAQUE
    case .Add:
        return gpu.BLEND_ADDITIVE
    case .Alpha:
        return gpu.BLEND_ALPHA
    case .Premultiplied_Alpha:
        return gpu.BLEND_PREMULTIPLIED_ALPHA
    }
    return gpu.BLEND_OPAQUE
}

_gpu_fill_mode :: proc(fill: Fill_Mode) -> (gpu.Cull_Mode, gpu.Fill_Mode) {
    switch fill {
    case .Front:
        return .Back, .Solid
    case .Back:
        return .Front, .Solid
    case .All:
        return .None, .Solid
    case .Wire:
        return .None, .Wireframe
    }
    return .Invalid, .Invalid
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Camera
//

Camera :: struct {
    pos:        Vec3,
    rot:        Quat,
    // View to clip transform.
    // NDC box is -1..1 on X and Y, and 0..1 on Z axis.
    projection: Mat4,
}

FRUSTUM_FAR_PLANE_INDEX :: 5

Frustum :: struct {
    planes:     [6]Vec4, // xyz normal, w offset
    corners:    [8]Vec3,
    bounds_min: Vec3,
    bounds_max: Vec3,
}

orthographic_projection :: proc(left, right, top, bottom, near, far: f32) -> (result: Mat4) {
    // D3D11, LH 0..1 NDC
    // https://learn.microsoft.com/en-us/windows/win32/direct3d9/d3dxmatrixorthooffcenterlh

    result[0, 0] = 2 / (right - left)
    result[1, 1] = 2 / (top - bottom)
    result[2, 2] = 1 / (far - near)
    result[0, 3] = (left + right) / (left - right)
    result[1, 3] = (top + bottom) / (bottom - top)
    result[2, 3] = near / (near - far)
    result[3, 3] = 1

    return result
}

// left handed reverse Z
// https://iolite-engine.com/blog_posts/reverse_z_cheatsheet
// NOTE: use Greater depth comparison!
perspective_projection :: proc(screen: Vec2, fov: f32, near: f32 = 0.01, far: f32 = 1000.0) -> (result: Mat4) {
    assert(fov > 0)
    assert(screen.x > 0)
    assert(screen.y > 0)

    aspect := screen.x / screen.y
    tan_half_fovy := math.tan(0.5 * fov)
    result[0, 0] = 1.0 / (tan_half_fovy * aspect)
    result[1, 1] = 1.0 / tan_half_fovy
    result[2, 2] = -near / (far - near)
    result[2, 3] = (far * near) / (far - near)
    result[3, 2] = 1

    return result
}

calc_camera_world_to_view_matrix :: proc(camera: Camera) -> (result: Mat4) {
    result =
        linalg.matrix4_from_quaternion_f32(linalg.quaternion_inverse(camera.rot)) *
        linalg.matrix4_translate_f32(-camera.pos)
    return result
}

calc_camera_world_to_clip_matrix :: proc(camera: Camera) -> (result: Mat4) {
    result = camera.projection * calc_camera_world_to_view_matrix(camera)
    return result
}

calc_camera_frustum :: proc(cam: Camera) -> Frustum {
    mvp := calc_camera_world_to_clip_matrix(cam)
    inv := linalg.matrix4_inverse_f32(mvp)
    return calc_matrix_frustum(inv)
}

calc_matrix_frustum :: proc(clip_to_world: Mat4) -> (result: Frustum) {
    // https://iquilezles.org/articles/frustumcorrect/
    // https://iquilezles.org/articles/frustum/

    fru := [8]Vec4{
        0 = clip_to_world * Vec4{-1, -1,  0, 1.0},
        1 = clip_to_world * Vec4{+1, -1,  0, 1.0},
        2 = clip_to_world * Vec4{-1, +1,  0, 1.0},
        3 = clip_to_world * Vec4{+1, +1,  0, 1.0},
        4 = clip_to_world * Vec4{-1, -1, +1, 1.0},
        5 = clip_to_world * Vec4{+1, -1, +1, 1.0},
        6 = clip_to_world * Vec4{-1, +1, +1, 1.0},
        7 = clip_to_world * Vec4{+1, +1, +1, 1.0},
    }

    for p, i in fru {
        result.corners[i] = p.xyz / p.w
    }

    result.bounds_min = max(f32)
    result.bounds_max = min(f32)

    for p in result.corners {
        result.bounds_min = linalg.min(result.bounds_min, p)
        result.bounds_max = linalg.max(result.bounds_max, p)
    }

    center: Vec3
    for p in result.corners {
        center += p
    }
    center *= 1.0 / 8.0

    result.planes = {
        _tri_plane(center, result.corners[4], result.corners[6], result.corners[5]),
        _tri_plane(center, result.corners[0], result.corners[4], result.corners[1]),
        _tri_plane(center, result.corners[2], result.corners[3], result.corners[6]),
        _tri_plane(center, result.corners[0], result.corners[2], result.corners[4]),
        _tri_plane(center, result.corners[1], result.corners[5], result.corners[3]),
        _tri_plane(center, result.corners[0], result.corners[1], result.corners[2]),
    }

    return result

    _tri_plane :: proc(center: Vec3, a, b, c: Vec3) -> Vec4 {
        normal := linalg.normalize0(linalg.cross(b - a, c - a))

        if linalg.dot(a - center, normal) < 0 {
            normal = -normal
        }

        return {
            normal.x,
            normal.y,
            normal.z,
            linalg.dot(normal, a),
        }
    }
}

is_box_in_frustum :: proc(fru: Frustum, pos: Vec3, rad: Vec3) -> bool #no_bounds_check {
    EPS :: 1

    if pos.x < fru.bounds_min.x - rad.x - EPS ||
       pos.y < fru.bounds_min.y - rad.y - EPS ||
       pos.z < fru.bounds_min.z - rad.z - EPS ||
       pos.x > fru.bounds_max.x + rad.x + EPS ||
       pos.y > fru.bounds_max.y + rad.y + EPS ||
       pos.z > fru.bounds_max.z + rad.z + EPS
    {
        return false
    }

    for plane in fru.planes {
        rad_on_normal := rad.x * abs(plane.x) + rad.y * abs(plane.y) + rad.z * abs(plane.z)
        dist := linalg.dot(plane.xyz, pos) - plane.w - rad_on_normal
        if dist > EPS {
            return false
        }
    }

    return true
}


// world_to_screen :: proc(pos: Vec3, cam: Camera) -> Vec3 {
//     cam_mvp := calc_camera_world_to_clip_matrix(cam)

//     p := cam_mvp * Vec4{pos.x, pos.y, pos.z, 1.0}
//     p.xyz /= p.w

//     // p.x = (p.x / f32(get_screen_size().x)) * 2.0 - 1.0
//     // p.y = 1.0 - 2.0 * (p.y / f32(get_screen_size().y))

//     unimplemented()

//     // return p
// }


// Returns the ray direction.
screen_to_world_ray :: proc(pos: Vec2, cam: Camera) -> Vec3 {
    cam_mvp := calc_camera_world_to_clip_matrix(cam)
    cam_inv := linalg.matrix4_inverse_f32(cam_mvp)

    p := pos

    p.x = (p.x / f32(get_screen_size().x)) * 2.0 - 1.0
    p.y = 1.0 - 2.0 * (p.y / f32(get_screen_size().y))

    p0 := cam_inv * Vec4{p.x, p.y, 0.0, 1.0}
    p1 := cam_inv * Vec4{p.x, p.y, 1.0, 1.0}
    p0.xyz /= p0.w
    p1.xyz /= p1.w

    return linalg.normalize0(p1.xyz - p0.xyz)
}




/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Misc
//

@(optimization_mode="favor_size")
hash_fnv64a :: proc "contextless" (data: []byte, seed: u64) -> u64 {
    h: u64 = seed
    for b in data {
        h = (h ~ u64(b)) * 0x100000001b3
    }
    return h
}

@(disabled=!LOG_INTERNAL)
log_internal :: proc(format: string, args: ..any, loc := #caller_location) {
    when LOG_INTERNAL {
        log.debugf(format, args = args, location = loc)
    }
}

_assertion_failure_proc :: proc(prefix, message: string, loc: runtime.Source_Code_Location) -> ! {
    // based on runtime.default_assertion_contextless_failure_proc

    runtime.print_caller_location(loc)
    runtime.print_string(" ")
    runtime.print_string(loc.procedure)
    runtime.print_string(": ")
    runtime.print_string(prefix)
    if len(message) > 0 {
        runtime.print_string(": ")
        runtime.print_string(message)
    }
    runtime.print_byte('\n')

    when ODIN_DEBUG {
        ctx := &_state.debug_trace_ctx
        if _state != nil && !debug_trace.in_resolve(ctx) {
            buf: [64]debug_trace.Frame
            runtime.print_string("Debug Stack Trace:\n")

            frames := debug_trace.frames(ctx, skip = 0, frames_buffer = buf[:])
            for f, i in frames {
                fl := debug_trace.resolve(ctx, f, context.temp_allocator)
                if fl.loc.file_path == "" && fl.loc.line == 0 {
                    continue
                }
                runtime.print_int(i)
                runtime.print_string(" : ")
                runtime.print_caller_location(fl.loc)
                runtime.print_string(" ")
                runtime.print_string(fl.loc.procedure)
                runtime.print_byte('\n')
            }
        }
    } else {
        runtime.print_string("    compile with -debug to show stack trace")
    }

    runtime.trap()
}



//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Counters
//
// A lightweight way to measure stats and report them to the outside world.
//

COUNTER_HISTORY :: 64

Counter_State :: struct {
    accum:      u64,
    vals:       [COUNTER_HISTORY]u64,
    total_num:  u64,
    total_min:  u64,
    total_max:  u64,
    total_sum:  u64,
}

Counter_Kind :: enum u8 {
    CPU_Frame_Ns,
    CPU_Frame_Work_Ns,
    Num_Draw_Calls,
    Num_Total_Instances,
    Num_Uploaded_Instances, // non-culled
    // TODO:
    // GPU_Frame_Ns
    // Upload_Ns,
    // Total_Draw_Layer_Ns,
    // Temp_Allocs,
    // Temp_Bytes,
}

_counter_add :: proc(kind: Counter_Kind, value: u64) {
    _state.counters[kind].accum = intrinsics.saturating_add(_state.counters[kind].accum, value)
}

_counter_flush :: proc(counter: ^Counter_State) {
    value := counter.accum
    counter.accum = 0
    if value == max(u64) {
        return
    }
    counter.total_num += 1
    counter.vals[counter.total_num % COUNTER_HISTORY] = value
    counter.total_min = min(counter.total_min, value)
    counter.total_max = max(counter.total_max, value)
    counter.total_sum += value
}

// Displays max of the recent history and a graph.
// Assumes screenspace camera.
// 'unit' is for converting e.g. nanoseconds into a reasonable range.
draw_counter :: proc(kind: Counter_Kind, pos: Vec3, scale: f32 = 1, unit: f32 = 1, col: Vec4 = 1, show_text := true) {
    scope_binds()
    bind_texture_by_handle(_state.default_font_texture)
    bind_blend(.Alpha)
    bind_depth_test(true)
    bind_depth_write(true)

    max_val: u64

    rect := Rect{
        min = {0, 1 - 1.0/128.0},
        max = {0 + 1.0/128.0, 1},
    }

    counter := _state.counters[kind]
    for i in 0..<COUNTER_HISTORY {
        index := (int(counter.total_num) - i) %% COUNTER_HISTORY
        val := counter.vals[index]

        height: f32 = scale * unit * f32(val)

        draw_sprite(
            pos = pos + {COUNTER_HISTORY - f32(i), height * 0.5, 0},
            rect = rect,
            scale = {1, height},
            col = col,
        )

        draw_sprite(
            pos = pos + {COUNTER_HISTORY - f32(i), height * 0.5, 0.01},
            rect = rect,
            scale = {3, height + 2},
            col = BLACK,
        )

        max_val = max(val, max_val)
    }

    if show_text {
        // last := counter.vals[counter.total_num % COUNTER_HISTORY]
        text: string
        if unit == 1 {
            text = ufmt.tprintf("%i", max_val)
        } else {
            text = ufmt.tprintf("%f", f64(max_val) * f64(unit))
        }

        // draw_text(, pos + {64 + 12, 0, 0}, col = col)
        draw_text(text, pos + {64 + 16, 4, 0}, col = col, scale = math.ceil_f32(_state.dpi_scale))
        draw_text(text, pos + {64 + 16 + 1, 4 - 1, 0.01}, col = BLACK, scale = math.ceil_f32(_state.dpi_scale))
    }
}



/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: Validation
//
// Ensures the data user passed in is in somewhat reasonable state.
//

@(disabled=!VALIDATION)
validate :: proc(cond: bool, msg := #caller_expression(cond), loc := #caller_location) {
    if !cond {
        // NOTE(bill): This is wrapped in a procedure call
        // to improve performance to make the CPU not
        // execute speculatively, making it about an order of
        // magnitude faster
        @(cold)
        internal :: #force_no_inline proc(msg: string, loc: runtime.Source_Code_Location) {
            p := context.assertion_failure_proc
            if p == nil {
                p = runtime.default_assertion_failure_proc
            }

            p("Raven: Validation Failed", message = msg, loc = loc)
        }
        internal(msg, loc)
    }
}


@(disabled = !VALIDATION)
validate_f32 :: #force_inline proc(x: f32, loc := #caller_location) {
    validate(x == x && (x * 0.5 != x || x == 0), "Value is NaN or Inf", loc = loc)
}

@(disabled = !VALIDATION)
validate_vec :: proc(v: [$N]f32, loc := #caller_location) {
    for x in v {
        validate_f32(x, loc)
    }
}

@(disabled = !VALIDATION)
validate_quat :: proc(q: quaternion128, loc := #caller_location) {
    validate_f32(q.x, loc)
    validate_f32(q.y, loc)
    validate_f32(q.z, loc)
    validate_f32(q.w, loc)
}


@(disabled = !VALIDATION)
validate_mat2 :: proc(m: Mat2, loc := #caller_location) {
    validate_vec(m[0], loc)
    validate_vec(m[1], loc)
}

@(disabled = !VALIDATION)
validate_mat3 :: proc(m: Mat3, loc := #caller_location) {
    validate_vec(m[0], loc)
    validate_vec(m[1], loc)
    validate_vec(m[2], loc)
}

@(disabled = !VALIDATION)
validate_mat4 :: proc(m: Mat4, loc := #caller_location) {
    validate_vec(m[0], loc)
    validate_vec(m[1], loc)
    validate_vec(m[2], loc)
    validate_vec(m[3], loc)
}

@(disabled = !VALIDATION)
validate_draw_sort_key :: proc(key: Draw_Sort_Key) {
    validate(key.texture != {} || key.texture_mode != .Non_Pooled)
    validate(key.ps != {})
    validate(key.vs != {})
}


/////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARK: CP437 encoding
// Extended ASCII encoding, all 256 characters are valid visual glyphs.
//
// https://en.wikipedia.org/wiki/Code_page_437
//

// Unicode -> CP437. Use when iterating over a string.
rune_to_char :: proc(r: rune) -> u8 {
    switch r {
    // ASCII
    case ' '..='~': return u8(r)

    case: fallthrough

    case '': return 0
    case '': return 1
    case '': return 2
    case '': return 3
    case '': return 4
    case '': return 5
    case '': return 6
    case '': return 7
    case '': return 8
    case '': return 9
    case '': return 10
    case '': return 11
    case '': return 12
    case '': return 13
    case '': return 14
    case '': return 15
    case '': return 16
    case '': return 17
    case '': return 18
    case '': return 19
    case '': return 20
    case '': return 21
    case '': return 22
    case '': return 23
    case '': return 24
    case '': return 25
    case '': return 26
    case '': return 27
    case '': return 28
    case '': return 29
    case '': return 30
    case '': return 31
    case '': return 127
    case '': return 128
    case '': return 129
    case '': return 130
    case '': return 131
    case '': return 132
    case '': return 133
    case '': return 134
    case '': return 135
    case '': return 136
    case '': return 137
    case '': return 138
    case '': return 139
    case '': return 140
    case '': return 141
    case '': return 142
    case '': return 143
    case '': return 144
    case '': return 145
    case '': return 146
    case '': return 147
    case '': return 148
    case '': return 149
    case '': return 150
    case '': return 151
    case '': return 152
    case '': return 153
    case '': return 154
    case '': return 155
    case '': return 156
    case '': return 157
    case '': return 158
    case '': return 159
    case '': return 160
    case '': return 161
    case '': return 162
    case '': return 163
    case '': return 164
    case '': return 165
    case '': return 166
    case '': return 167
    case '': return 168
    case '': return 169
    case '': return 170
    case '': return 171
    case '': return 172
    case '': return 173
    case '': return 174
    case '': return 175
    case '': return 176
    case '': return 177
    case '': return 178
    case '': return 179
    case '': return 180
    case '': return 181
    case '': return 182
    case '': return 183
    case '': return 184
    case '': return 185
    case '': return 186
    case '': return 187
    case '': return 188
    case '': return 189
    case '': return 190
    case '': return 191
    case '': return 192
    case '': return 193
    case '': return 194
    case '': return 195
    case '': return 196
    case '': return 197
    case '': return 198
    case '': return 199
    case '': return 200
    case '': return 201
    case '': return 202
    case '': return 203
    case '': return 204
    case '': return 205
    case '': return 206
    case '': return 207
    case '': return 208
    case '': return 209
    case '': return 210
    case '': return 211
    case '': return 212
    case '': return 213
    case '': return 214
    case '': return 215
    case '': return 216
    case '': return 217
    case '': return 218
    case '': return 219
    case '': return 220
    case '': return 221
    case '': return 222
    case '': return 223
    case '': return 224
    case '': return 225
    case '': return 226
    case '': return 227
    case '': return 228
    case '': return 229
    case '': return 230
    case '': return 231
    case '': return 232
    case '': return 233
    case '': return 234
    case '': return 235
    case '': return 236
    case '': return 237
    case '': return 238
    case '': return 239
    case '': return 240
    case '': return 241
    case '': return 242
    case '': return 243
    case '': return 244
    case '': return 245
    case '': return 246
    case '': return 247
    case '': return 248
    case '': return 249
    case '': return 250
    case '': return 251
    case '': return 252
    case '': return 253
    case '': return 254
    case 0x00A0: return 255 // non breaking space
    }
}

// CP437 -> Unicode. Use when iterating over encoded text to print it.
char_to_rune :: proc(ch: u8) -> rune {
    switch ch {
    // ASCII
    case '!'..='~':
        return rune(ch)

    case: fallthrough
    case 0: return ''
    case 1: return ''
    case 2: return ''
    case 3: return ''
    case 4: return ''
    case 5: return ''
    case 6: return ''
    case 7: return ''
    case 8: return ''
    case 9: return ''
    case 10: return ''
    case 11: return ''
    case 12: return ''
    case 13: return ''
    case 14: return ''
    case 15: return ''
    case 16: return ''
    case 17: return ''
    case 18: return ''
    case 19: return ''
    case 20: return ''
    case 21: return ''
    case 22: return ''
    case 23: return ''
    case 24: return ''
    case 25: return ''
    case 26: return ''
    case 27: return ''
    case 28: return ''
    case 29: return ''
    case 30: return ''
    case 31: return ''
    case 127: return ''
    case 128: return ''
    case 129: return ''
    case 130: return ''
    case 131: return ''
    case 132: return ''
    case 133: return ''
    case 134: return ''
    case 135: return ''
    case 136: return ''
    case 137: return ''
    case 138: return ''
    case 139: return ''
    case 140: return ''
    case 141: return ''
    case 142: return ''
    case 143: return ''
    case 144: return ''
    case 145: return ''
    case 146: return ''
    case 147: return ''
    case 148: return ''
    case 149: return ''
    case 150: return ''
    case 151: return ''
    case 152: return ''
    case 153: return ''
    case 154: return ''
    case 155: return ''
    case 156: return ''
    case 157: return ''
    case 158: return ''
    case 159: return ''
    case 160: return ''
    case 161: return ''
    case 162: return ''
    case 163: return ''
    case 164: return ''
    case 165: return ''
    case 166: return ''
    case 167: return ''
    case 168: return ''
    case 169: return ''
    case 170: return ''
    case 171: return ''
    case 172: return ''
    case 173: return ''
    case 174: return ''
    case 175: return ''
    case 176: return ''
    case 177: return ''
    case 178: return ''
    case 179: return ''
    case 180: return ''
    case 181: return ''
    case 182: return ''
    case 183: return ''
    case 184: return ''
    case 185: return ''
    case 186: return ''
    case 187: return ''
    case 188: return ''
    case 189: return ''
    case 190: return ''
    case 191: return ''
    case 192: return ''
    case 193: return ''
    case 194: return ''
    case 195: return ''
    case 196: return ''
    case 197: return ''
    case 198: return ''
    case 199: return ''
    case 200: return ''
    case 201: return ''
    case 202: return ''
    case 203: return ''
    case 204: return ''
    case 205: return ''
    case 206: return ''
    case 207: return ''
    case 208: return ''
    case 209: return ''
    case 210: return ''
    case 211: return ''
    case 212: return ''
    case 213: return ''
    case 214: return ''
    case 215: return ''
    case 216: return ''
    case 217: return ''
    case 218: return ''
    case 219: return ''
    case 220: return ''
    case 221: return ''
    case 222: return ''
    case 223: return ''
    case 224: return ''
    case 225: return ''
    case 226: return ''
    case 227: return ''
    case 228: return ''
    case 229: return ''
    case 230: return ''
    case 231: return ''
    case 232: return ''
    case 233: return ''
    case 234: return ''
    case 235: return ''
    case 236: return ''
    case 237: return ''
    case 238: return ''
    case 239: return ''
    case 240: return ''
    case 241: return ''
    case 242: return ''
    case 243: return ''
    case 244: return ''
    case 245: return ''
    case 246: return ''
    case 247: return ''
    case 248: return ''
    case 249: return ''
    case 250: return ''
    case 251: return ''
    case 252: return ''
    case 253: return ''
    case 254: return ''
    case 255: return 0x00A0 // non breaking space
    }
}